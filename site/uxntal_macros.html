<!DOCTYPE html><html><head>
<meta charset="utf-8"/><meta name="viewport" content="width=device-width, inital-scale=1"/><link href="../links/main.css" type="text/css" rel="stylesheet"/><link href="../media/services/icon.png" type="image/png" rel="shortcut icon"/>
<title>XXIIVV &mdash; uxntal macros</title></head><body>
<header><a href="home.html"><img alt="XXIIVV" src="../media/icon/logo.svg"></a></header>
<nav><ul><li><a href="uxntal.html" class="parent">uxntal</a></li><li><a href="varvara.html">varvara</a></li></ul><ul><li><a href="uxntal_cheatsheet.html">uxntal cheatsheet</a></li><li><a href="uxntal_reference.html">uxntal reference</a></li><li><a href="uxntal_stacking.html">uxntal stacking</a></li><li><a href="uxntal_macros.html" class="self">uxntal macros</a></li></ul><ul></ul></nav><main><h2>A collection of commonly used macros in uxn projects.</h2>

<h2>Generics</h2>

<p>Shorthands for logic and arithmetic opcodes.</p>

<pre>
%+  { ADD }  %-  { SUB }  %*  { MUL }  %/  { DIV }
%<  { LTH }  %>  { GTH }  %=  { EQU }  %!  { NEQ }
%++ { ADD2 } %-- { SUB2 } %** { MUL2 } %// { DIV2 }
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }
</pre>

<h3>Fast Arithmetic</h3>

<pre>
%2*  { #10 SFT } %2/  { #01 SFT } %2**  { #10 SFT2 } %2//  { #01 SFT2 }
%4*  { #20 SFT } %4/  { #02 SFT } %4**  { #20 SFT2 } %4//  { #02 SFT2 }
%8*  { #30 SFT } %8/  { #03 SFT } %8**  { #30 SFT2 } %8//  { #03 SFT2 }
%10* { #40 SFT } %10/ { #04 SFT } %10** { #40 SFT2 } %10// { #04 SFT2 }
%20* { #50 SFT } %20/ { #05 SFT } %20** { #50 SFT2 } %20// { #05 SFT2 }

%2MOD  { #01 AND } %2MOD2  { #0001 AND2 }
%4MOD  { #03 AND } %4MOD2  { #0003 AND2 }
%8MOD  { #07 AND } %8MOD2  { #0007 AND2 }
%10MOD { #0f AND } %10MOD2 { #000f AND2 }
</pre>

<h3>Buttons</h3>

<pre>
%B_UP { #10 } %B_DOWN { #20 } %B_LEFT   { #40 } %B_RIGHT { #80 }
%B_A  { #01 } %B_B    { #02 } %B_SELECT { #04 } %B_START { #08 }
</pre>

<h3>Binary Opcodes</h3>

<pre>
%ROL { DUP #07 SFT SWP #10 SFT + }
%ROR { DUP #70 SFT SWP #01 SFT + }
%ROL2 { DUP2 #0f SFT2 SWP2 #10 SFT2 ++ }
%ROR2 { DUP2 #f0 SFT2 SWP2 #01 SFT2 ++ }
</pre>

<h3>Signed Opcodes</h3>

<pre>
%LTS2 { #8000 ++ SWP2 #8000 ++ >> }
%GTS2 { #8000 ++ SWP2 #8000 ++ << }
%ABS2 { #0000 OVR2 #0f EQU JMP SWP2 SUB2 }
</pre>

<h3>Modulo/MinMax</h3>

<pre>
%MOD  { DIVk MUL SUB }
%MOD2 { DIV2k MUL2 SUB2 }
%MIN2 { LTH2k JMP SWP2 POP2 }
%MAX2 { GTH2k JMP SWP2 POP2 }
</pre>

<h3>ASCII</h3>

<p>To test for character types.</p>

<pre>
%IS-UC { DUP #40 > SWP #5b < AND }
%IS-LC { DUP #60 > SWP #7b < AND }
%IS-NUM { DUP #2f > SWP #3a < AND }
</pre>

<h2>Numbers</h2>

<p>To emit hexadecimal numbers:</p>
<pre>
@print ( short* -- )

	&short ( short* -- ) SWP ,&byte JSR
	&byte ( byte -- ) DUP #04 SFT ,&char JSR
	&char ( char -- ) #0f AND DUP #09 GTH #27 MUL ADD #30 ADD #18 DEO

JMP2r
</pre>

<p>To emit a decimal number from a short:</p>
<pre>
@print-dec ( value* -- )

	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	,&emit JSR POP

JMP2r
	&parse DIV2k DUP ,&emit JSR MUL2 SUB2 JMP2r
	&emit #30 ADD #18 DEO JMP2r
</pre>

<p>To emit a decimal number from a short, without leading zeroes:</p>
<pre>
@print-dec-trim ( value* -- )

	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	,&emit JSR POP

JMP2r
	&parse DIV2k DUP ,&emit JSR MUL2 SUB2 JMP2r
	&emit 
		DUP [ LIT &zero $1 ] #0000 EQU2 ,&skip JCN
		#01 ,&zero STR DUP #30 ADD #18 DEO &skip POP JMP2r
</pre>

<h3>decimal string to hex number</h3>
<pre>
@sdec ( addr* -- hex* )

	LIT2r 0000
	&while
		LDAk #30 SUB #00 SWP
		STH2r #000a MUL2 ADD2 STH2
		INC2 LDAk ,&while JCN
	POP2
	STH2r

JMP2r
</pre>

<pre>
@print-zeropage ( -- )

	#0a #18 DEO
	#0000
	&loop
		LDZk ;print-hex/byte JSR2
		DUP #01 AND #00 = ,&no-space JCN
			#2018 DEO
			&no-space
		DUP #0f AND #0f ! ,&no-break JCN
			#0a18 DEO
			&no-break
		INC NEQk ,&loop JCN
	POP2
	#0a #18 DEO

JMP2r
</pre>

<h2>Strings</h2>

<p>To emit a string: </p>
<pre>
@print-str ( str* -- )

	&while
		LDAk #18 DEO
		INC2 LDAk ,&while JCN
	POP2

JMP2r
</pre>

<h3>string primitives</h3>
<pre>
@scat ( src* dst* -- )

	DUP2 ,slen JSR ADD2

@scpy ( src* dst* -- )
	
	STH2
	&while
		LDAk STH2kr STA INC2r
		INC2 LDAk ,&while JCN
	POP2
	#00 STH2r STA

JMP2r

@slen ( str* -- len* )

	DUP2 ,scap JSR SWP2 SUB2

JMP2r

@scap ( str* -- end* )

	LDAk #00 NEQ JMP JMP2r
	&while INC2 LDAk ,&while JCN

JMP2r

@sput ( char str* -- )

	,scap JSR STA

JMP2r

@spop ( str* -- )

	LDAk ,&no-null JCN
		POP2 JMP2r &no-null
	#00 ROT ROT ,scap JSR #0001 SUB2 STA

JMP2r
</pre>

<h3>Memory</h3>

<pre>
@mclr ( addr* len* -- )

	OVR2 ADD2 SWP2
	&loop
		STH2k #00 STH2r STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

JMP2r

@mcpy ( src* dst* len* -- )

	SWP2 STH2
	OVR2 ADD2 SWP2
	&loop
		LDAk STH2kr STA INC2r
		INC2 GTH2k ,&loop JCN
	POP2 POP2
	POP2r

JMP2r
</pre>

<h3>Random</h3>

<pre>
@prng-init ( -- )

	( seed )
	#00 .DateTime/second DEI
	#00 .DateTime/minute DEI #60 SFT2 EOR2
	#00 .DateTime/hour   DEI #c0 SFT2 EOR2 ,prng/x STR2
	#00 .DateTime/hour   DEI #04 SFT2
	#00 .DateTime/day    DEI #10 SFT2 EOR2
	#00 .DateTime/month  DEI #60 SFT2 EOR2
		.DateTime/year  DEI2 #a0 SFT2 EOR2 ,prng/y STR2

JMP2r

@prng ( -- number* )

	LIT2 &x $2
	DUP2 #50 SFT2 EOR2
	DUP2 #03 SFT2 EOR2
	LIT2 &y $2 DUP2 ,&x STR2
	DUP2 #01 SFT2 EOR2 EOR2
	,&y STR2k POP

JMP2r
</pre>

<pre>
@print-arvelie ( -- )

	.DateTime/year DEI2 #07d6 -- NIP
		( digit1 ) DUP #0a DIV #30 + #18 DEO
		( digit2 ) #0a MOD #30 + #18 DEO
	.DateTime/doty DEI2
		( month ) DUP2 #000e DIV2 NIP #41 + #18 DEO
		( digit3 ) #000e MOD2 DUP2 #000a DIV2 NIP #30 + #18 DEO
		( digit4 ) #000a MOD2 NIP #30 + #18 DEO
	#0a #18 DEO

JMP2r
</pre>

<h3>Presets</h3>

<pre>
%AUTO-NONE   { #00 .Screen/auto DEO }
%AUTO-X      { #01 .Screen/auto DEO }
%AUTO-Y      { #02 .Screen/auto DEO }
%AUTO-XY     { #03 .Screen/auto DEO }
%AUTO-ADDR   { #04 .Screen/auto DEO }
%AUTO-XADDR  { #05 .Screen/auto DEO }
%AUTO-YADDR  { #06 .Screen/auto DEO }
%AUTO-XYADDR { #07 .Screen/auto DEO }
%RELEASE-MOUSE { #0096 DEO }
</pre>

<h3>Geometry</h3>

<pre>
%SIZE-TO-RECT {
	STH2 STH2 OVR2 STH2r ++ OVR2 STH2r ++
} ( x y w h -- x1 y1 x2 y2 )
</pre>

<h3>Shorthands</h3>

<p>Small notorious optimizations for conditional flags and jumps:</p>

<table border="1">
	<tr><th>#0101 EQU2</th><td>AND</td></tr>
	<tr><th>#0000 NEQ2</th><td>ORA</td></tr>
	<tr><th>JSR2 JMP2r</th><td>JMP2</td></tr>
</table>
<ul></ul></main>
<footer><a href="https://creativecommons.org/licenses/by-nc-sa/4.0" target="_blank"><img src="../media/icon/cc.svg" alt="CreativeCommons"/></a> <a href="https://webring.xxiivv.com/" target="_blank"><img src="../media/icon/webring.svg" alt="Webring"/></a> <a href="https://merveilles.town/@neauoire" rel="me" target="_blank"><img src="../media/icon/merveilles.svg" alt="Merveilles"/></a> <a href="ethics.html"><img src="../media/icon/dreipfeile.svg" alt="NoNazis!"/></a> <a href="uxn.html"><img src="../media/icon/uxn.svg" alt="UxnPowered"/></a> <span><a href="devine_lu_linvega.html">Devine Lu Linvega</a> &copy; 2022 &mdash; <a href="about.html">BY-NC-SA 4.0</a></span></footer>
</body></html>