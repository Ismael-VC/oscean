( Oh time thy pyramids )

|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2 &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0000

|0100 ( -> )

	( clear old copies )
	;sitemap-path .File2/name DEO2
	#01 .File2/delete DEO
	;calendar-path .File2/name DEO2
	#01 .File2/delete DEO
	;journal-path .File2/name DEO2
	#01 .File2/delete DEO

	( setup output )
	#01 .File2/append DEO
	#0001 .File2/length DEO2

	( lexicon )
	;sitemap-path .File2/name DEO2 ;ul-tag ;<w> JSR2
	;parse-lexicon JSR2
	;sitemap-path .File2/name DEO2 ;ul-tag ;</w> JSR2

	( diary )
	;calendar-path .File2/name DEO2 ;header-txt ;w-str JSR2 ;p-tag ;<w> JSR2
	;parse-diary-pages JSR2
	;calendar-path .File2/name DEO2 ;p-tag ;</w> JSR2

	( halt. )
	#010f DEO

BRK

(
@|lexicon )

@parse-lexicon ( -- )

	;lexicon-path .File/name DEO2
	#2000 .File/length DEO2
	;lexicon .File/read DEO2
	( swap ) ;lexicon #0a00 ;cswp JSR2

	;lexicon .File/success DEI2 ADD2k NIP2 SWP2
	&l
		,parse-lexicon-line JSR
		INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@parse-lexicon-line ( line* -- next* )

	INC2 INC2
	DUP2 ,parse-term JSR
	;scap JSR2

JMP2r

@parse-term ( term* -- )

	;sitemap-path .File2/name DEO2
	;li-tag ;<w> JSR2
	;w-link JSR2
	;li-tag ;</w> JSR2
	#0a ;w-chr JSR2

JMP2r

(
@|diary )

@parse-diary-pages ( -- )

	#4000 .File/length DEO2
	;diary-path-pages/end ;diary-path-pages
	&l
		LDA2k .File/name DEO2
		;workarea .File/read DEO2
		#0000
			( to* from* ) ;workarea STH2k .File/success DEI2 ADD2
			( cap ) STA2k NIP2
			( parse ) STH2r ,parse-diary-page JSR
		INC2 INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@parse-diary-page ( to* from* -- )

	( swap ) DUP2 #0a00 ;cswp JSR2
	&l
		,parse-diary-line JSR
		INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@parse-diary-line ( line* -- next* )

	DUP2 ;parse-log JSR2
	( split )
	DUP2 #0900 ;cswp JSR2
	( event )
	DUP2 #0005 ADD2 LDA LIT "+ NEQ ,&no-event JCN
		DUP2 ;parse-event JSR2
		&no-event
	( photo )
	;no-photo OVR2 #0006 ADD2 ;scmp JSR2 ,&no-photo JCN
		DUP2 ;parse-photo JSR2
		&no-photo
	( eol )
	;scap JSR2 INC2
	;scap JSR2 INC2
	;scap JSR2

JMP2r

@parse-log ( event* -- )

	DUP2 #000a ADD2 ;make-path-tmp JSR2 .File2/name DEO2
	;w-str JSR2
	#0a ;w-chr JSR2

JMP2r

@parse-event ( event* -- )

	;calendar-path .File2/name DEO2

	( year )
	LDAk2 ,&last LDR2 EQU2 ,&no-year JCN
		;br-tag ;<w/> JSR2
		LDA2k ,&last STR2
		&no-year

	( date )
	;code-tag ;<w> JSR2
		DUP2 #0005 ;w-len JSR2
	;code-tag ;</w> JSR2
	;scap JSR2 INC2
	( term ) DUP2 ;w-link JSR2
	;spacer ;w-str JSR2
	;scap JSR2 INC2
	( name ) ;w-str JSR2
	;br-tag ;<w/> JSR2
	#0a ;w-chr JSR2

JMP2r
	&last $3

@parse-photo ( photo* -- )

	( limit to 20 entries )
	[ LIT2 &len $2 ] #0020 LTH2 ,&continue JCN
		POP2 JMP2r
		&continue

	;journal-path .File2/name DEO2

	DUP2 #0006 ADD2 ,&pict STR2
	#000a ADD2
		DUP2 ,&term STR2 
		;scap JSR2 INC2 ,&title STR2

	;figure-tag ;<w> JSR2
		( src ) [ LIT2 &pict $2 ]
		( alt ) [ LIT2 &title $2 ] ;w-img JSR2
		;figcaption-tag ;<w> JSR2
			[ LIT2 &term $2 ] ;w-link JSR2
				;spacer ;w-str JSR2
				,&title LDR2 ;w-str JSR2
		;figcaption-tag ;</w> JSR2
	;figure-tag ;</w> JSR2
	,&len LDR2 INC2 ,&len STR2
	#0a ;w-chr JSR2

JMP2r

@make-path-tmp ( name* -- path* )

	;&term ;scpy-line JSR2
	;&term #20 LIT "_ ;cswp JSR2
	;&path

JMP2r
	&path "tmp/log- &term $30

(
@|framework )

@<w> ( name* -- )

	LIT "< ;w-chr JSR2
	;w-str JSR2
	LIT "> ;w-chr JSR2

JMP2r

@</w> ( name* -- )

	LIT "< ;w-chr JSR2
	LIT "/ ;w-chr JSR2
	;w-str JSR2
	LIT "> ;w-chr JSR2

JMP2r

@<w/> ( name* -- )

	LIT "< ;w-chr JSR2
	;w-str JSR2
	LIT "/ ;w-chr JSR2
	LIT "> ;w-chr JSR2

JMP2r

@w-img ( src* alt* )

	;&a ;w-str JSR2
	;w-str JSR2
	;&b ;w-str JSR2
	;w-str JSR2
	;&c ;w-str JSR2

JMP2r
	&a "<img 20 "alt=" $1
	&b "" 20 "src="../media/diary/ $1
	&c ".jpg"/> $1

@w-link ( str* -- )

	;&a ;w-str JSR2
	DUP2 ;w-path JSR2
	;&b ;w-str JSR2
	;w-str JSR2
	;&c ;w-str JSR2

JMP2r
	&a "<a 20 "href=' $1
	&b ".html'> $1
	&c "</a> $1

@w-path ( str* -- )

	&w
		LDAk LIT "_ OVR #20 NEQ [ JMP SWP POP ] ,w-chr JSR
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@w-len ( str* len* -- )

	ADD2k NIP2 SWP2
	&l
		LDAk ,w-chr JSR
		INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@w-str ( str* -- )

	&w
		LDAk ,w-chr JSR
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@w-chr ( chr -- )

	;&b STH2k STA
	STH2r .File2/write DEO2

JMP2r
	&b $1

(
@|stdlib )

@cswp ( str* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

JMP2r

@scpy-line ( src* dst* -- ) STH2 &w LDAk STH2kr STA INC2r INC2 LDAk #1f GTH ,&w JCN POP2 #00 STH2r STA JMP2r
@scmp ( a* b* -- f ) STH2 &l LDAk LDAkr STHr ANDk #00 EQU ,&e JCN NEQk ,&e JCN POP2 INC2 INC2r ,&l JMP &e NIP2 POP2r EQU JMP2r
@scap ( str* -- end* ) LDAk #00 NEQ JMP JMP2r &w INC2 LDAk ,&w JCN JMP2r
@print ( hex* -- ) SWP ,&b JSR &b DUP #04 SFT ,&c JSR &c #0f AND DUP #09 GTH #27 MUL ADD #30 ADD #18 DEO JMP2r
@print-str ( str* -- ) &w LDAk #18 DEO INC2 LDAk ,&w JCN POP2 JMP2r

(
@|assets )

@p-tag "p $1
@ul-tag "ul $1
@li-tag "li $1
@br-tag "br $1
@code-tag "code $1
@figure-tag "figure $1
@figcaption-tag "figcaption $1

@lexicon-path "src/tables/lexicon $1
@sitemap-path "src/htm/sitemap.htm $1
@calendar-path "src/htm/calendar.htm $1
@journal-path "src/htm/journal.htm $1

@spacer 20 "&mdash; 20 $1

@no-photo "000 $1

@diary-paths
	&3 "src/tables/diary/15-19 $1
	&2 "src/tables/diary/10-14 $1
	&1 "src/tables/diary/05-09 $1
	&0 "src/tables/diary/00-04 $1

@diary-path-pages
	:diary-paths/3
	:diary-paths/2
	:diary-paths/1
	:diary-paths/0
	&end

@header-txt
3c68 323e 5468 6520 4361 6c65 6e64 6172
2073 686f 7773 2075 7063 6f6d 696e 6720
616e 6420 7061 7374 2065 7665 6e74 7320
6672 6f6d 2074 6865 206a 6f75 726e 616c
2e3c 2f68 323e 0a0a 3c70 3e54 6869 7320
7769 6b69 2075 7365 7320 7468 6520 3c61
2068 7265 663d 2761 7276 656c 6965 2e68
746d 6c27 3e41 7276 656c 6965 3c2f 613e
2074 696d 6520 666f 726d 6174 2c20 7768
6572 6520 7468 6520 7965 6172 2069 7320
6469 7669 6465 6420 696e 2032 3620 7065
7269 6f64 732c 206f 7220 3c69 3e6d 6f6e
7468 733c 2f69 3e2c 206f 6620 3134 2064
6179 732c 206e 756d 6265 7265 6420 6672
6f6d 2041 2074 6f20 5a2e 2054 6865 2069
6e69 7469 616c 206c 6f67 6769 6e67 2079
6561 7220 616e 6420 7468 6520 4172 7665
6c69 6520 6461 7465 7320 636f 756e 7420
7570 7761 7264 2066 726f 6d20 3230 3036
2e20 596f 7520 6361 6e20 7365 6520 6d6f
7265 2075 7064 6174 6573 2069 6e20 7468
6520 3c61 2068 7265 663d 276a 6f75 726e
616c 2e68 746d 6c27 3e6a 6f75 726e 616c
3c2f 613e 2061 6e64 203c 6120 6872 6566
3d27 6e6f 772e 6874 6d6c 273e 6e6f 773c
2f61 3e20 7061 6765 733c 2f70 3e0a 0a00

(
@|memory )

@lexicon $2000

@workarea
