( Oh time thy pyramids )

|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @File2 &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

|0000

|0100 ( -> )

	( setup output )
	;output-path .File2/name DEO2
	#0001 .File2/length DEO2

	,parse-pages JSR
	( halt. )
	#010f DEO

BRK
@output-path "src/htm/calendar.htm $1


@parse-pages ( -- )

	#4000 .File/length DEO2
	;diary-path-pages/end ;diary-path-pages
	&l
		LDA2k .File/name DEO2
		;workarea
			DUP2 .File/read DEO2
			#0000 OVR2 .File/success DEI2 ADD2 STA2
			,parse-diary JSR
		#4000 ;workarea ;mclr JSR2
		INC2 INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@parse-diary ( page* -- )

	( swap ) DUP2 #0a00 ;cswp JSR2
	&w
		,parse-line JSR
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@parse-line ( line* -- line* )

	( swap ) DUP2 #0900 ;cswp JSR2

	DUP2 #0005 ADD2 LDA LIT "+ NEQ ,&end JCN

	( year )
	LDAk2 ,&last LDR2 EQU2 ,&no-year JCN
		LDAk #18 DEO INC2k LDA #18 DEO #0a18 DEO
		LDA2k ,&last STR2
		&no-year

	DUP2 #00 ROT ROT #0005 ADD2 STA
	( print )
	( date ) DUP2 ;print-str JSR2 #2018 DEO
	;scap JSR2 INC2
	;scap JSR2 INC2
	( term ) DUP2 ;print-str JSR2 #2018 DEO
	;scap JSR2 INC2
	( name ) DUP2 ;print-str JSR2 #0a18 DEO
	;scap JSR2

JMP2r

	( end )
	&end
	;scap JSR2 INC2
	;scap JSR2 INC2 
	;scap JSR2

JMP2r
	&last $3

(
@|framework )

@w-str ( str* -- )

	&w
		LDAk ,&b STR
		;&b .File2/write DEO2
		INC2 LDAk ,&w JCN
	POP2

JMP2r
	&b $1

(
@|stdlib )

@cswp ( str* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

JMP2r

@sfnd ( src* target* -- addr* )

	STH2
	&while
		STH2kr OVR2 ,sseg JSR #00 EQU ,&continue JCN
			POP2r JMP2r
			&continue
		INC2 LDAk ,&while JCN
	POP2
	POP2r
	#ffff

JMP2r

@scap ( str* -- end* ) LDAk #00 NEQ JMP JMP2r &w INC2 LDAk ,&w JCN JMP2r
@sput ( chr str* -- ) ,scap JSR INC2k #00 ROT ROT STA STA JMP2r
@slen ( str* -- len* ) DUP2 ,scap JSR SWP2 SUB2 JMP2r
@scat ( src* dst* -- ) ,scap JSR
@scpy ( src* dst* -- ) STH2 &w LDAk STH2kr STA INC2r INC2 LDAk ,&w JCN POP2 #00 STH2r STA JMP2r
@scmp ( a* b* -- f ) STH2 &l LDAk LDAkr STHr ANDk #00 EQU ,&e JCN NEQk ,&e JCN POP2 INC2 INC2r ,&l JMP &e NIP2 POP2r EQU JMP2r
@sseg ( a* b* -- f ) STH2 &l LDAk LDAkr STHr NEQ ,&e JCN INC2k LDA #00 EQU ,&e JCN INC2 INC2r ,&l JMP &e LDA LDAr STHr EQU JMP2r
@sclr ( str* -- ) LDAk ,&w JCN POP2 JMP2r &w STH2k #00 STH2r STA INC2 LDAk ,&w JCN POP2 JMP2r
@mclr ( src* len* -- ) OVR2 ADD2 SWP2 &l STH2k #00 STH2r STA INC2 GTH2k ,&l JCN POP2 POP2 JMP2r
@mcpy ( src* dst* len* -- ) SWP2 STH2 OVR2 ADD2 SWP2 &loop LDAk STH2kr STA INC2r INC2 GTH2k ,&loop JCN POP2 POP2 POP2r JMP2r
@print ( hex* -- ) SWP ,&b JSR &b DUP #04 SFT ,&c JSR &c #0f AND DUP #09 GTH #27 MUL ADD #30 ADD #18 DEO JMP2r
@print-str ( str* -- ) &w LDAk #18 DEO INC2 LDAk ,&w JCN POP2 JMP2r

@print-stat ( number* type* -- )

	;print-dec JSR2
	#2018 DEO
	,print-str JSR
	#0a18 DEO

JMP2r

@print-error ( name* msg* -- )

	#0a18 DEO
	SWP2
	,print-str JSR
	#2018 DEO
	,print-str JSR
	#0d18 DEO
	#0a18 DEO
	#010f DEO

BRK

@print-dec ( short* -- )

	#00 ,&z STR
	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	NIP
	&emit
		DUP [ LIT &z $1 ] EQU ,&skip JCN
			#ff ,&z STR DUP #30 ADD #18 DEO
			&skip
	POP

JMP2r
	&parse
		DIV2k DUP ,&emit JSR MUL2 SUB2
	JMP2r

(
@|assets )

@dict
	&lexicon-txt "lexicon $1
	&terms-txt "terms $1
	&links-txt "links $1
	&diaries-txt "diaries $1
	&http-txt "http $1
	&href-attr "href $1
	&htm-ext ".htm $1
	&log-ext "-log $1
	&map-ext "-map $1
	&lexicon-path "src/tables/lexicon $1
	&htm-path "src/htm/ $1
	&tmp-path "tmp/ $1

@diary-paths
	&3 "src/tables/diary/15-19 $1
	&2 "src/tables/diary/10-14 $1
	&1 "src/tables/diary/05-09 $1
	&0 "src/tables/diary/00-04 $1

@diary-path-pages
	:diary-paths/3
	:diary-paths/2
	:diary-paths/1
	:diary-paths/0
	&end

@error
	&size "-- 20 "size_exceeded 20 $1
	&diary-term-missing "-- 20 "diary_term_missing 20 $1
	&include-missing "-- 20 "include_missing 20 $1
	&include-large "-- 20 "include_too_large 20 $1
	&local-missing "-- 20 "local_missing 20 $1
	&duplicate-outgoing "-- 20 "duplicate_outgoing 20 $1

(
@|memory )

@lexicon $2000
@outgoing $1000

@header-txt
3c68 323e 5468 6520 4361 6c65 6e64 6172
2073 686f 7773 2075 7063 6f6d 696e 6720
616e 6420 7061 7374 2065 7665 6e74 7320
6672 6f6d 2074 6865 206a 6f75 726e 616c
2e3c 2f68 323e 0a0a 3c70 3e54 6869 7320
7769 6b69 2075 7365 7320 7468 6520 3c61
2068 7265 663d 2761 7276 656c 6965 2e68
746d 6c27 3e41 7276 656c 6965 3c2f 613e
2074 696d 6520 666f 726d 6174 2c20 7768
6572 6520 7468 6520 7965 6172 2069 7320
6469 7669 6465 6420 696e 2032 3620 7065
7269 6f64 732c 206f 7220 3c69 3e6d 6f6e
7468 733c 2f69 3e2c 206f 6620 3134 2064
6179 732c 206e 756d 6265 7265 6420 6672
6f6d 2041 2074 6f20 5a2e 2054 6865 2069
6e69 7469 616c 206c 6f67 6769 6e67 2079
6561 7220 616e 6420 7468 6520 4172 7665
6c69 6520 6461 7465 7320 636f 756e 7420
7570 7761 7264 2066 726f 6d20 3230 3036
2e20 596f 7520 6361 6e20 7365 6520 6d6f
7265 2075 7064 6174 6573 2069 6e20 7468
6520 3c61 2068 7265 663d 276a 6f75 726e
616c 2e68 746d 6c27 3e6a 6f75 726e 616c
3c2f 613e 2061 6e64 203c 6120 6872 6566
3d27 6e6f 772e 6874 6d6c 273e 6e6f 773c
2f61 3e20 7061 6765 733c 2f70 3e0a 0a00

@workarea
