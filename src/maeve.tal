( Oscean's preprocessor )

~utils.tal

|0000

	@maxlength $2
	@stats
		&terms $2
		&links $2
		&diaries $2

|0100 @on-reset

	( append mode )
	#01 .File/append DEO

	( load lexicon )
	;lexicon-path .File/name DEO2
	#2000 .File/length DEO2
	;lexicon
		DUP2 .File/read DEO2
		REMOVE-LINEBREAKS

	#f000 ;workarea -- .maxlength STZ2

	( let's fucking go )
	;parse-lexicon JSR2
	.stats/terms LDZ2 ;print-dec JSR2 #20 EMIT ;terms-txt PRINT
	.stats/links LDZ2 ;print-dec JSR2 #20 EMIT ;links-txt PRINT
	;parse-diary JSR2
	.stats/diaries LDZ2 ;print-dec JSR2 #20 EMIT ;diaries-txt PRINT

HALT

( lexicon )

@parse-lexicon ( term* -- )

	;lexicon
	&while
		( move to name ) INC2 INC2
		( setup files ) DUP2 ,parse-term JSR
		( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	POP2

RTN

@parse-term ( term* -- )
	
	( htm )
	;&htm STH2k #0030 ;mclr JSR2
	;htm-path STH2kr ;scpy JSR2
	DUP2 STH2kr ;scat JSR2
	;htm-ext STH2kr ;scat JSR2
	;&htm #20 [ LIT '_ ] ;cswp JSR2

	( clear outgoing )
	;outgoing #1000 ;mclr JSR2

	( load htm )
	STH2r .File/name DEO2
	.maxlength LDZ2 .File/length DEO2
	;workarea .File/read DEO2
	( null terminate )
	#00 ;workarea .File/success DEI2 ++ STA

	( errors on empty )
	.File/success DEI2 #0000 >> ,&continue JCN
		;error-include-missing SWP2 ERROR
		&continue

	;workarea ,validate JSR

	( stats ) .stats/terms LDZ2k INC2 ROT STZ2

	POP2

RTN
	&htm $30

@validate ( page* -- )

	&while
		;href-attr OVR2 ;scmp-seg JSR2 #00 = ,&continue JCN
			( move to href ) #0006 ++
			( strip relative ) LDAk [ LIT '. ] = ,&continue JCN
			( strip jumps ) LDAk [ LIT '# ] = ,&continue JCN
			( strip http* ) DUP2 ;http-txt SWP2 ;scmp-seg JSR2 ,&continue JCN
			;&link STH2k #0030 ;mclr JSR2
			DUP2 STH2kr #002f ;mcpy JSR2
			STH2kr [ LIT '. ] #00 ;cswp JSR2
			STH2kr [ LIT '_ ] #20 ;cswp JSR2
			( find term )
			STH2r ,find-term JSR #0000 !! ,&found JCN
				;&link ;error-broken-link ERROR
				&found
			( connect incoming )
			OVR2 ;&link ,connect-incoming JSR
			&continue
		INC2 LDAk ,&while JCN
	POP2

RTN
	&link $30

@find-term ( name* -- term* )

	STH2
	;lexicon
	&while
		( move to name ) INC2 INC2
		( test ) DUP2 STH2kr ;scmp JSR2 ,&end JCN
		( move to eol ) &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	POP2 #0000
	&end
	POP2r

RTN

@connect-incoming ( src* dst* -- )

	( check for duplicates )
	;outgoing OVR2 ;sfnd JSR2 #ffff == ,&no-duplicate JCN
		;warning-duplicate-outgoing ;print-str JSR2
		OVR2 ;print-str JSR2 #2f EMIT DUP2 PRINT
		&no-duplicate

	( record in outgoing )
	DUP2 ;outgoing ;scat JSR2
	;outgoing #0a ;sput JSR2

	( find file )
	;&dst-map STH2k #0030 ;mclr JSR2
	;tmp-path STH2kr ;scpy JSR2
	STH2kr ;scat JSR2
	;map-ext STH2kr ;scat JSR2
	STH2kr #20 [ LIT '_ ] ;cswp JSR2

	( write )
	STH2r .File/name DEO2
	DUP2 ;slen JSR2 .File/length DEO2
	.File/write DEO2

	#0001 .File/length DEO2
	;&lb .File/write DEO2

	( stats ) .stats/links LDZ2k INC2 ROT STZ2

RTN
	&dst-map $30
	&lb 0a

( diary )

@parse-diary ( -- )

	;diary-path-pages/end ;diary-path-pages
	&loop
		( clear workarea )
		;workarea .maxlength LDZ2 ;mclr JSR2
		LDA2k .File/name DEO2
		.maxlength LDZ2 .File/length DEO2
		;workarea .File/read DEO2
		( split lines )
		;workarea REMOVE-LINEBREAKS
		;workarea ,parse-diary-page JSR
		INC2 INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@parse-diary-page ( page* -- )

	&while
		DUP2 ,&log STR2
		#000a ++
		( bound ) DUP2 #09 ;next-char JSR2 STH2
		( clip ) #00 STH2kr STA
		( flag ) DUP2 ;find-term JSR2 ,&term STR2
		( unclip ) #09 STH2r STA
		[ LIT2 &log $2 ] [ LIT2 &term $2 ] ,connect-diaries JSR
		( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	POP2

RTN

@connect-diaries ( diary* term* -- )

	( find file )
	;&dst-log STH2k #0030 ;mclr JSR2
	;tmp-path STH2kr ;scpy JSR2
	STH2kr ;scat JSR2
	;log-ext STH2kr ;scat JSR2
	STH2kr #20 [ LIT '_ ] ;cswp JSR2
	( write )
	STH2r .File/name DEO2
	DUP2 ;slen JSR2 .File/length DEO2
	.File/write DEO2
	( linebreak )
	#0001 .File/length DEO2
	;&lb .File/write DEO2

	( stats ) .stats/diaries LDZ2k INC2 ROT STZ2

RTN
	&dst-log $30
	&lb 0a

~stdlib.tal

@terms-txt "terms $1
@links-txt "links $1
@diaries-txt "diaries $1

@http-txt "http $1

@href-attr "href $1

@htm-ext ".htm $1
@log-ext "-log $1
@map-ext "-map $1

@lexicon-path "tables/lexicon $1

@diary-paths
	&3 "tables/diary/15-19 $1
	&2 "tables/diary/10-14 $1
	&1 "tables/diary/05-09 $1
	&0 "tables/diary/00-04 $1

@diary-path-pages
	:diary-paths/3
	:diary-paths/2
	:diary-paths/1
	:diary-paths/0
	&end

@htm-path "htm/ $1
@tmp-path "tmp/ $1

@error-include-missing "-- 20 "include_missing 20 $1
@error-broken-link "-- 20 "local_missing 20 $1
@warning-duplicate-outgoing "-- 20 "duplicate_outgoing 20 $1

@lexicon $2000
@outgoing $1000
@workarea
