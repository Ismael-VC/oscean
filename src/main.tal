( The Bequest Globe )

%+  { ADD } %-   { SUB }              %/   { DIV }  
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ } 
%++ { ADD2 } %-- { SUB2 }              %// { DIV2 } 
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }  

%DEBUG  { ;print-hex JSR2 #0a .Console/write DEO }
%DEBUG2 { SWP ;print-hex JSR2 ;print-hex JSR2 #0a .Console/write DEO }

%RTN { JMP2r }
%FILE { #4000 }

%^< { ;add-open-tag JSR2 }
%^> { ;add-close-tag JSR2 }
%^<> { ;add-wrap-text JSR2 }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset-hs $2 &offset-ls $2 &name   $2 &length $2 &load $2 &save   $2 ]
|b0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000

( program )

|0100 ( -> )

	( load file )
	;tree/source .File/name DEO2
	#4000        .File/length DEO2
	;tree/data   .File/load DEO2
	.File/success DEI2 ;tree/length STA2

	;parse JSR2

	( end )
	#01 .System/debug DEO
	#01 .System/halt DEO
	
BRK

@parse ( -- )

	;tree/data 
	( remove linebreaks )
	DUP2 #0a #00 ;cswp JSR2
	&while
		( move to name ) #0002 ++ 
		( build ) DUP2 ;build-page JSR2
		( move to eol )  DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2

RTN

( ---------------------------------- )

@filepath $30

@build-page ( name* -- )

	( create file )
	#00 FILE STA

	;doctype FILE ;scat JSR2
	( create body )
	;html-tag ^<
		( head )
		;build-page-head JSR2
		( body )
		;body-tag ^<
			;build-page-header JSR2
			;build-page-nav JSR2
			;build-page-main JSR2
			;build-page-footer JSR2
		;body-tag ^>
	;html-tag ^>

	POP2

	( create filepath )
	DUP2
	#00 ;filepath STA
	;output-path ;filepath ;scpy JSR2
	( name* ) ;filepath ;scat JSR2
	;html-ext ;scat JSR2
	;filepath #20 LIT '_ ;cswp JSR2

	( save file )
	;filepath       .File/name DEO2
	FILE ;slen JSR2 .File/length DEO2
	FILE            .File/save DEO2

RTN

@build-page-head ( -- )

	;head-tag ^<
		;stylesheet-txt ;stylesheet-type ;stylesheet-path ;add-link-tag JSR2
		;shortcuticon-txt ;shortcuticon-type ;shortcuticon-path ;add-link-tag JSR2
		;title-tag ^<
			;title FILE ;scat JSR2
		;title-tag ^>
	;head-tag ^>

RTN

@build-page-header ( -- )

	;home-path ;add-a-tag JSR2
		;logo-path ;logo-alt-txt ;add-img-tag JSR2
	;a-tag ^>

RTN

@build-page-nav ( -- )

	;nav-tag ^<

	;nav-tag ^>

RTN

@build-page-main ( -- )
	
	;main-tag ^<
		;header-txt ;h1-tag ^<>
		;hello-uxn-txt ;p-tag ^<>
	;main-tag ^>

RTN

@build-page-footer ( -- )

	;footer-tag ^<
		;creativecommon-path ;add-a-external-tag JSR2
			;creativecommon-icon-path ;creativecommon-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;webring-path ;add-a-external-tag JSR2
			;webring-icon-path ;webring-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;merveilles-path ;add-a-external-tag JSR2
			;merveilles-icon-path ;merveilles-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;span-tag ^<
			;devinelulinvega-path ;add-a-tag JSR2
				;devinelulinvega-txt FILE ;scat JSR2
			;a-tag ^>
			#20 FILE ;ccat JSR2
			;copy-entity-txt FILE ;scat JSR2
			#20 FILE ;ccat JSR2
			;year-txt FILE ;scat JSR2
			#20 FILE ;ccat JSR2
			;mdash-entity-txt FILE ;scat JSR2
			#20 FILE ;ccat JSR2
			;about-path ;add-a-tag JSR2
				;license-txt FILE ;scat JSR2
			;a-tag ^>
		;span-tag ^>
	;footer-tag ^>

RTN

@add-wrap-text ( string* tag* -- )

	STH2k ^<
	;add-text JSR2
	STH2r ^>

RTN

@add-text ( string* -- )

	FILE ;scat JSR2

RTN

@add-open-tag ( tag* -- )

	STH2
	LIT '< FILE ;ccat JSR2
	STH2r FILE ;scat JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-close-tag ( tag* -- )

	STH2
	LIT '< FILE ;ccat JSR2
	LIT '/ FILE ;ccat JSR2
	STH2r FILE ;scat JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-a-tag ( path* -- )

	STH2
	LIT '< FILE ;ccat JSR2
	LIT 'a FILE ;ccat JSR2
	#20 FILE ;ccat JSR2
	STH2r ;href-attr ;add-attr JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-a-external-tag ( path* -- )

	STH2
	LIT '< FILE ;ccat JSR2
	LIT 'a FILE ;ccat JSR2
	#20 FILE ;ccat JSR2
	STH2r ;href-attr ;add-attr JSR2
	#20 FILE ;ccat JSR2
	;blank-target ;target-attr ;add-attr JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-img-tag ( src* alt* -- )
	
	STH2 STH2
	LIT '< FILE ;ccat JSR2
	;img-tag FILE ;scat JSR2
	#20 FILE ;ccat JSR2
	STH2r ;src-attr ;add-attr JSR2
	#20 FILE ;ccat JSR2
	STH2r ;alt-attr ;add-attr JSR2
	LIT '/ FILE ;ccat JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-link-tag ( rel* type* href* -- )
	
	LIT '< FILE ;ccat JSR2
	;link-tag FILE ;scat JSR2
		#20 FILE ;ccat JSR2
		;href-attr ;add-attr JSR2
		#20 FILE ;ccat JSR2
		;type-attr ;add-attr JSR2
		#20 FILE ;ccat JSR2
		;rel-attr ;add-attr JSR2
	LIT '/ FILE ;ccat JSR2
	LIT '> FILE ;ccat JSR2

RTN

@add-attr ( attr* body* -- )
 
	FILE ;scat JSR2
	LIT '= FILE ;ccat JSR2
	LIT '" FILE ;ccat JSR2
	FILE ;scat JSR2
	LIT '" FILE ;ccat JSR2

RTN

( generics )

@cswp ( string* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

RTN

@next-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr EQU ,&end JCN
		LDAk #00 = ,&end JCN
		INC2 LDAk ,&while
	&end
		POPr

RTN

( helpers )

@ccat ( char dst* -- )
	
	#0001 --
	&while
		INC2 LDAk ,&while JCN
	STH2k STA
	#00 STH2r INC2 STA

RTN

@chex ( char -- hex )
	
	DUPk #2f GTH SWP #3a LTH #0101 == ,&num JCN
	DUPk #40 GTH SWP #47 LTH #0101 == ,&uca JCN
	DUPk #60 GTH SWP #67 LTH #0101 == ,&lca JCN
	POP #00 RTN
	&num ( char -- hex )
		LIT '0 - RTN
	&uca ( char -- hex )
		LIT 'A - #0a + RTN
	&lca ( char -- hex )
		LIT 'a - #0a + RTN

RTN

@scat ( src* dst* -- )

	DUP2 ,slen JSR ++ ,scpy JSR

RTN

@scpy ( src* dst* -- )

	STH2 
	DUP2
	&while
		SWP2k -- STH2kr ADD2 STH2
		LDAk STH2r STA
		INC2 LDAk ,&while JCN
	SWP2 -- STH2r ADD2 
	#00 ROT ROT STA

RTN

@slen ( addr* -- length* )

	#0001 -- DUP2
	&while
		INC2 LDAk ,&while JCN
	SWP2 --
	#0001 --

RTN

@print-hex ( value* -- )
	
	STHk #04 SFT ,&parse JSR .Console/write DEO
	STHr #0f AND ,&parse JSR .Console/write DEO
	RTN
	&parse ( value -- char )
		DUP #09 GTH ,&above JCN #30 ADD RTN &above #09 SUB #60 ADD RTN

RTN

@print-string ( addr* -- )
	
	#0001 --
	&loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&loop JCN
	POP2
	( linebreak ) #0a .Console/write DEO 

RTN

@output-path "../site-new/ $1
@html-ext ".html $1

@title "XXIIVV $1

@doctype "<!DOCTYPE 20 "html> $1
@html-tag "html $1
@head-tag "head $1
@title-tag "title $1
@body-tag "body $1
@nav-tag "nav $1
@main-tag "main $1
@footer-tag "footer $1
@h1-tag "h1 $1
@img-tag "img $1
@p-tag "p $1
@a-tag "a $1
@span-tag "span $1
@link-tag "link $1

@target-attr "target $1
@src-attr "src $1
@alt-attr "alt $1
@href-attr "href $1
@blank-target "_blank $1
@rel-attr "rel $1
@type-attr "type $1

@logo-alt-txt "XXIIVV $1
@header-txt "header $1
@year-txt "2021 $1

@index-path "../site-new/index.html $1
@home-path "home.html $1
@logo-path "../media/icon/logo.svg $1
@hello-uxn-txt "Hello 20 "Uxn! $1

@stylesheet-txt "stylesheet $1
@stylesheet-type "text/css $1
@stylesheet-path "../links/main.css $1

@shortcuticon-txt "shortcut 20 "icon $1
@shortcuticon-type "image/png $1
@shortcuticon-path "../media/services/icon.png $1

@about-path "about.html $1
@license-txt "BY-NC-SA 20 "4.0 $1
@copy-entity-txt "&copy; $1
@mdash-entity-txt "&mdash; $1

@devinelulinvega-path "devine_lu_linvega.html $1
@devinelulinvega-txt "Devine 20 "Lu 20 "Linvega $1

@creativecommon-path "https://creativecommons.org/licenses/by-nc-sa/4.0 $1
@creativecommon-icon-path "../media/icon/cc.svg $1
@creativecommon-alt-txt "CreativeCommons $1
@webring-path "http://webring.xxiivv.com/ $1
@webring-icon-path "../media/icon/webring.svg $1
@webring-alt-txt "Webring $1
@merveilles-path "https://merveilles.town/@neauoire $1
@merveilles-icon-path "../media/icon/merveilles.svg $1
@merveilles-alt-txt "Merveilles $1

@buff $100

@tree
	&source "database/tree.tbtl $1
	&length $2
	&data 
