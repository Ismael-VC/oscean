<h2>Modal is a language based a model of computation that uses pattern matching and replacement to rewrite trees.</h2>

<p>All Modal code is represented as a series of rules applied to a given tree, formatted textually as tokens delimited with parenthesis, which gets continually modified until no rules match any given part of the tree.</p>

<p>A pattern/replacement can be:</p>

<ul>
	<li><b>A token</b>: foo, bar, baz</li>
	<li><b>A variable</b>: ?foo, ?bar, ?baz</li>
	<li><b>A sequence of the above</b>: (foo bar baz), (foo ?bar baz), foo (bar (baz))</li>         
</ul>

<p>A replacement can only use variables that have been used in
a pattern. When a variable is used in a pattern, and when we try
to match a given tree with a pattern, any variables are bound to their
corresponding tokens/sequences in said tree.
    For example, consider the following:</p>

<pre>define (square ?x) (?x * ?x)
</pre>
<p>This defines a rule with the pattern `square ?x`. Let's try to
apply this pattern to a given tree.</p>

<pre>
square (square (25))
</pre>
<p>Upon feeding this particular tree to Modal, Modal will run through
the list of rules that have been defined and try to match each rule's pattern
with the tree. Modal will crawl along the tree using a hybrid of depth-first
and breadth-first search. When Modal tries to apply the rule to the above tree,
the variable...</p>

<pre>
?x
</pre>

<p>...is bound, and it now has the value...</p>
<pre>
(square (25))
</pre>
<p>From here, now that Modal has found a match, it eliminates part of the
tree that it matched and replaces it with the right-hand side of a given rule.
If the above rule is applied, we end up with a new tree:</p>
<pre>
(square (25)) * (square (25))
</pre>
<p>From here, we run through our defined rules once more, and we end up with
the following tree:</p>
<pre>
((25) * (25)) * ((25) * (25))
</pre>
<p>This is then further reduced using built-in operations. I encourage you to
try it for yourself. Modal always works on a single global tree, and patterns
are applied to various points in that tree. Modal traverses the tree by storing
its textual form in a queue, and at any given moment, Modal tries to match the
head of that queue with a rule from its supplied ruleset.</p>

<p>Q: How do I write common structures, like loops, conditional statements, etc.?<br />
A: Loops map to recursive patterns, and because Modal has a "loose" syntax,
   you can write your own control flow structures.</p>
<pre>
Let's go through an example.

define (if      (true)  ?branch)      (?branch)
define (if      (false) ?branch)      ()
define (if/else (true)  ?true ?false) (?true)
define (if/else (false) ?true ?false) (?false)
</pre>
<p>The above patterns define both a single and double branch expression.
The latter is an example of a ternary statement, where the 'else' branch is
always required. These two control structures can be combined with boolean
operations, as defined below.</p>
<pre>
define ((true)  and (true))  (true)
define ((true)  and (false)) (false)
define ((false) and (true))  (false)
define ((false) and (false)) (false)

define ((true)  or (true))  (true)
define ((true)  or (false)) (true)
define ((false) or (true))  (true)
define ((false) or (false)) (false)

define (not (true))  (false)
define (not (false)) (true)
</pre>
<p>The above defines truth tables for conditional statements. We can use
these in conjunction with if and if/else to form arbitrarily nested conditions.</p>
<pre>
if (
        if/else (
                    (true) and ((false) or (true))
                )
                (
                    not (true)
                )
                (
                    not (false)
                )
   )
   (
        (true) and (true)
   )
</pre>
<p>Here's the above, in a more compact form.</p>
<pre>
if
    (if/else
        ((true) and ((false) or (true)))
        (not (true))
        (not (false)))
    ((true) and (true))
</pre>
<p>However, there's a problem. Due to Modal's queue-based execution,
if the condition for if and if/else isn't fully reduced to 'true' or 'false',
Modal skips over it entirely and evaluates the branches. This leads to some
unexpected behavior, as you end up performing unneeded calculations and
selecting between them at a later point.</p>
<p>It's possible to circumvent this. By defining multiple patterns,
you can use Modal's pattern matching features to perform conditional checks.
Let's try to define the factorial pattern using this method.</p>
<pre>
define (factorial (num 1)) (num 1)
define (factorial (num ?x))  ((num ?x) * (factorial ((num ?x) - (1))))
</pre>
<p>Here, we've written two possible branches for our factorial pattern
in the form of two rules. Since Modal will always use rules defined prior
in the ruleset, we can organize rules according to priority, such that
earlier rules match prior to later ones. This allows us to write
complex conditionals that would otherwise be impossible to write properly.</p>

<p>I hope this has inspired you to give Modal a try. This was a small
taste of what Modal has to offer. Modal's prelude (prelude.modal) includes
a lot of built-in rules that you can use when writing code. It also includes
some examples of custom syntax.</p>

<h3>Prelude</h3>

<pre>
1 -> (s (0));


neg (neg ?x) -> ?x;
neg (0)      -> 0;


add (s ?x) (s ?y) -> s (add ?x (s ?y));
add (0)    (s ?x) -> s ?x;
add (s ?x) (0)    -> s ?x;
add (0)    (0)    -> 0;


?x + ?y + ?z -> (?x + ?y) + ?z;


?x + ?y -> add ?x ?y;


sub (s ?x) (s ?y) -> sub ?x ?y;
sub (s ?x) (0)    -> s ?x;
sub (0)    (s ?x) -> neg (s ?x);
sub (0)    (0)    -> 0;


?x - ?y -> sub ?x ?y;


mul (s ?x) (s ?y)  -> (s ?x) + (mul (s ?x) ((s ?y) - 1));
mul (s ?x) (s (0)) -> s ?x;
mul (s ?x) (0)     -> 0;
mul (0)    (s ?x)  -> 0;


?x * ?y -> mul ?x ?y;


Ensures that a list or a number has been reduced to its normal form. ;

reduced (0)       -> true;
reduced (nil)     -> true;
reduced (s ?x)    -> reduced ?x;
reduced (?h : ?t) -> reduced ?t;
reduced ?x        -> false;


Because there may be conflicts with expressions that
are currently being reduced, we need to fold over reduced
lists, i.e ones that have already been fully generated. ;

fold       (?f) ?i ?l           -> fold reduced ?l (?f) ?i ?l;
fold true  (?f) ?i (nil)        -> ?i;
fold true  (?f) ?i (?h : (nil)) -> ?f ?i ?h;
fold true  (?f) ?i (?h : ?t)    -> ?f ?i (fold (?f) ?h ?t);
fold false (?f) ?i ?l           -> fold (?f) ?i ?l;


factorial (s (0)) -> s (0);
factorial (s ?x)  -> (s ?x) * (factorial ((s ?x) - 1));


sum (?h : ?t) -> fold (add) (0) (?h : ?t);


range ?x (s (0)) -> ?x : (nil);
range ?x (s ?y)  -> ?x : (range (?x + 1) ((s ?y) - 1));


Disgusting (yet valid) hack for currying.
We need lambdas. ;

unpack (?h : nil) -> ?h;
unpack (?h : ?t)  -> ?h unpack ?t;
unpack (?x)       -> ?x;
unpack (?x .      -> ?x unpack (;


:: (?f) ?a -> ?f unpack ?a;


mapp (?f) ?a (nil)        -> nil;
mapp (?f) ?a (?h : (nil)) -> (?f unpack ?a ?h) : (nil);
mapp (?f) ?a (?h : ?t)    -> (?f unpack ?a ?h) : (mapp (?f) ?a ?t);


map (?f) (nil)      -> nil;
map (?f) (?h : nil) -> (?f ?h) : (nil);
map (?f) (?h : ?t)  -> (?f ?h) : (map (?f) ?t);


product ?x -> fold (mul) 1 ?x;


factorial2 ?x -> product (range 1 ?x);


contains ?x (nil)     -> false;
contains ?x (?x : ?t) -> true;
contains ?x (?h : ?t) -> contains ?x ?t;


unique       (nil)     -> nil;
unique false (?h : ?t) -> ?h : (unique ?t);
unique true  (?h : ?t) -> unique ?t;
unique       (?h : ?t) -> unique contains ?h ?t (?h : ?t);


length (nil)     -> 0;
length (?h : ?t) -> s (length ?t);


zipWith (?f) (nil)       (nil)       -> nil;
zipWith (?f) (?h : ?t)   (nil)       -> nil;
zipWith (?f) (nil)       (?h : ?t)   -> nil;
zipWith (?f) (?h1 : ?t1) (?h2 : ?t2) -> (?f ?h1 ?h2) : (zipWith (?f) ?t1 ?t2);


evens ?x -> zipWith (add) (range (0) ?x) (range (0) ?x);


not (not ?x) -> ?x;
not true     -> false;
not false    -> true;


any ?x -> contains true ?x;


all ?x -> not contains false ?x;


none ?x -> not any ?x;


add1 ?x -> add ?x 1;


square ?x -> ?x * ?x;


reduce (s ?x)    -> s (reduce ?x);
reduce (0)       -> (0);
reduce (?h : ?t) -> ?h : (reduce ?t);
reduce (nil)     -> nil;</pre>

<ul>
	<li><a href='https://wryl.tech/' target='_blank'>wryl</a>, Immediate Mode Technologies(as of 2019).</li>
</ul>