( The Bequest Globe )

%+  { ADD } %-   { SUB }              %/   { DIV }  
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ } 
%++ { ADD2 } %-- { SUB2 }              %// { DIV2 } 
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }  

%1-- { #0001 -- }
%2++ { INC2 INC2 }

%DEBUG  { ;print-hex/byte JSR2 #0a .Console/write DEO }
%DEBUG2 { ;print-hex/short JSR2 #0a .Console/write DEO }
%PRINT { ;print-string JSR2 #0a .Console/write DEO }
%PRINT-CR { ;print-hex/short JSR2 #0d .Console/write DEO }

%RTN { JMP2r }
%HALT { #0101 .System/debug DEO2 }
%ERROR { ;error JMP2 }

%GET-DEPTH { DUP2 #0002 -- LDA ;chex JSR2 }
%REMOVE-LINEBREAKS { #0a #00 ;cswp JSR2 }
%REWIND { #00 ;prev-char JSR2 INC2 }

%^s { FILE ;scat JSR2 }
%^c { FILE ;ccat JSR2 }
%^space { #20 ^c }
%^< { STH2 LIT '< ^c STH2r ^s LIT '> ^c }
%^> { STH2 LIT '< ^c LIT '/ ^c STH2r ^s LIT '> ^c }
%^<> { STH2k ^< ^s STH2r ^> }

( thread )
%PUTS { ;put/string JSR2 }
%PUTC { ;put/char JSR2 }
%CLOSE { #00 ROT ROT STA }
%RESUME { ;scap JSR2 }
%open-tag { STH2 LIT '< PUTC STH2r PUTS LIT '> PUTC }
%close-tag { STH2 LIT '< PUTC LIT '/ PUTC STH2r PUTS LIT '> PUTC }

( Map:
	0x1800 - 0x3000 Lexicon
	0x3000 - 0x8000 Diary
	0x8000 - 0xffff Output )

%TERM-NAME-SIZE { #0016 }

%LEXICON { #1800 }
%LEXICON-SIZE { #1800 }
%DIARY { #3000 }
%DIARY-SIZE { #5000 }
%FILE { #8000 }
%FILE-SIZE { #8000 }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset-hs $2 &offset-ls $2 &name   $2 &length $2 &load $2 &save   $2 ]
|b0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000 $2 

@term
	&name $16 ( term name limit )
	&pict $4
	&diary $4c

@buff $40

( program )

|0100 ( -> )
	
	;load JSR2
	,focus-page LDR ;build-one JCN2
		;build-all JMP2
	HALT
	
BRK

@focus-page ( "index ) $1

@error ( name* msg* -- )
	
	SWP2 
	1--
	&name-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&name-loop JCN
	POP2
	#20 .Console/write DEO 
	1--
	&msg-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&msg-loop JCN
	POP2
	#0a .Console/write DEO 

	HALT

RTN

@load ( -- )
	
	;lexicon-path ,&lexicon JSR
	RTN
	&lexicon ( path* -- )
		( load lexicon )
		.File/name DEO2
		LEXICON-SIZE  .File/length DEO2
		LEXICON STH2k .File/load DEO2
		STH2r REMOVE-LINEBREAKS
	RTN
	&diary ( path* -- )
		DIARY DIARY-SIZE ;mclr JSR2
		( load diary )
		.File/name DEO2
		DIARY-SIZE  .File/length DEO2
		DIARY STH2k .File/load DEO2
		STH2r REMOVE-LINEBREAKS
	RTN

RTN

@build-one ( -> ) 

	;focus-page ;find-term JSR2 ;build-page JSR2
	HALT

BRK

@build-all ( -- )
	
	LIT2r 0000
	LEXICON
	&while
		( move to name ) 2++ 
		( display ) STH2kr PRINT-CR INC2r
		( build ) DUP2 ,build-page JSR
		( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	POP2 POP2r
	HALT

RTN

( ---------------------------------- )

@build-page ( name* -- )
	
	( cleanup )
	( zero-page ) #0000 #0100 ;mclr JSR2
	( output ) FILE FILE-SIZE ;mclr JSR2

	( term name limit )
	DUP2 ;slen JSR2 TERM-NAME-SIZE << ,&namelength-ok JCN
		;error-term-name-size ERROR
		&namelength-ok

	( fill name )
	DUP2 ;term/name ;scpy JSR2

	( fill diary )
	DUP2 ;find-diary JSR2
	DUP2 #0000 == ,&continue JCN
		DUP2 ;term/diary #0050 ;mcpy JSR2
		( copy pict )
		DUP2 #0006 ++
		( clip bound ) DUP2 #0003 ++ STH2
		( clip start ) #00 STH2kr STA
		( clip wrap ) ;term/pict ;scpy JSR2
		( clip restore ) #09 STH2r STA
		&continue
	POP2

	;doctype ^s
	( create body )
	;html-tag ^<
		( head )
		;build-head JSR2
		( body )
		;body-tag ^<
			;build-page-header JSR2
			;build-page-nav JSR2
			;build-page-main JSR2
			;build-page-footer JSR2
		;body-tag ^>
	;html-tag ^>

	( check file )
	FILE ;slen JSR2 FILE-SIZE << ,&size-ok JCN
		;error-output-size ERROR
		&size-ok

	( save file )
	;to-output-path JSR2 .File/name DEO2
	FILE ;slen JSR2   .File/length DEO2
	FILE              .File/save DEO2
	FILE ;check-page JSR2

RTN

@build-head ( FILE* name* -- name* )

	;head-tag ^<
		;stylesheet-txt ;stylesheet-type ;stylesheet-path ;add-link-tag JSR2
		;shortcuticon-txt ;shortcuticon-type ;shortcuticon-path ;add-link-tag JSR2
		( TODO: add og )
		;title-tag ^<
			;title-txt ^s ^space ;mdash-entity-txt ^s ^space DUP2 ^s
		;title-tag ^>
	;head-tag ^>

RTN

@build-page-header ( name* -- name* )

	;header-tag ^<
		;home-path ;add-a-tag JSR2
			;logo-path ;title-txt ;add-img-tag JSR2
		;a-tag ^>
	;header-tag ^>

RTN

@build-page-nav ( name* -- name* )

	;nav-tag ^<
		GET-DEPTH #00 = ,&no-siblings JCN
		( parents ) 
		GET-DEPTH #02 < ,&no-parents JCN
			DUP2 ;find-parent JSR2 ;find-parent JSR2 ,&children JSR POP2
			&no-parents
		( siblings ) 
		GET-DEPTH #01 < ,&no-siblings JCN
			DUP2 ;find-parent JSR2 ,&children JSR POP2
			&no-siblings
		( children ) ,&children JSR
	;nav-tag ^>
	RTN

	&children ( name* -- name* )

		;ul-tag ^<
		DUP2
		( stash depth ) GET-DEPTH STH
		( start after ) DUP2 ;slen JSR2 ++ INC2 
		&while
			( stop at sibling ) LDAk ;chex JSR2 STHkr = ,&end JCN
			( when depth+1 ) LDAk ;chex JSR2 STHkr INC NEQ ,&continue JCN
				;li-tag ^<
				DUP2 2++ ;add-local JSR2
				;li-tag ^>
				&continue
			( move to eol ) 2++ &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		&end
		POP2 POPr
		;ul-tag ^>

RTN

@build-page-main ( name* -- name* )
	
	;main-tag ^<
		( picture )
		;term/pict LDA #00 = ,&no-picture JCN
			;term/diary ;build-module-photo JSR2
			&no-picture
		( body )
		;build-page-main-body JSR2
		( modules )
		DUP2 1-- LDA [ LIT ', ] ! ,&no-album JCN 
			;build-module-album JSR2
			&no-album
		DUP2 1-- LDA [ LIT '; ] ! ,&no-portal JCN 
			;build-module-portal JSR2
			&no-portal
		DUP2 1-- LDA [ LIT ': ] ! ,&no-archive JCN 
			;build-module-archive JSR2
			&no-archive
		( special pages )
		DUP2 ;index-txt ;scmp JSR2 #00 = ,&no-index JCN
			;build-special-index JSR2
			&no-index
		DUP2 ;calendar-txt ;scmp JSR2 #00 = ,&no-calendar JCN
			;build-special-calendar JSR2
			&no-calendar
		DUP2 ;journal-txt ;scmp JSR2 #00 = ,&no-journal JCN
			;build-special-journal JSR2
			&no-journal
		;build-module-calendar JSR2
	;main-tag ^>

RTN

@build-page-main-body ( name* -- name* )

	( max include size )
	#8000 FILE DUP2 ;slen JSR2 ++ #8000 -- -- STH2k

	#0000 !! ,&no-null JCN
		;error-include-size ERROR
		&no-null

	( write body )
	DUP2 ;to-include-path JSR2 .File/name DEO2
	STH2kr .File/length DEO2
	FILE DUP2 ;slen JSR2 ++ STH2k .File/load DEO2

	.File/success DEI2 SWP2r STH2r !! ,&no-cap JCN
		;error-include-size ERROR
		&no-cap

	.File/success DEI2 #0000 !! ,&no-blank JCN
		;error-include-missing ERROR
		&no-blank

	( close ) #00 STH2r .File/success DEI2 ++ STA

RTN

@build-special-index ( -- )

	;ul-tag ^<
		LEXICON 
		&while
			( move to name ) 2++ 
			( push thread ) FILE RESUME
			;li-tag open-tag
				OVR2 ;thread-local JSR2
			;li-tag close-tag
			#00 PUTC
			( pop thread ) POP2
			( move to eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		POP2 
	;ul-tag ^>
	
RTN

@build-special-calendar ( -- )

	;ul-tag ^<
		;diary-path-pages/end ;diary-path-pages
		&loop
			LDA2k ;load/diary JSR2 ,&append-li JSR
			INC2 INC2 GTH2k ,&loop JCN
		POP2 POP2
	;ul-tag ^>

	RTN

	&append-li ( name* -- name* )
		DIARY 
		&while
			DUP2 #0005 ++ LDA [ LIT '+ ] ! ;&continue JCN2
				( break list on year change )
				DUP2 INC2 LDA [ STHk ] ;&last LDA = ,&no-yearchange JCN
					;ul-tag ^>
					;ul-tag ^<
					&no-yearchange
				;li-tag ^<
					DUP2 ;build-module-event JSR2
				;li-tag ^>
				( save last )
				[ STHr ] ;&last STA
				&continue
			( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ;&while JCN2
		POP2

RTN
	&last $1

@build-special-journal ( -- )

	#00 ;&limit STA
	;diary-path-page2 ;load/diary JSR2 
	,&append-figure JSR
	;diary-path-page1 ;load/diary JSR2 
	,&append-figure JSR
	;diary-path-page0 ;load/diary JSR2 
	,&append-figure JSR
	RTN

	&append-figure ( name* -- name* )
		DIARY 
		&while
			;&limit LDA #20 > ,&end JCN
			DUP2 #0006 ++ ;no-picture SWP2 ;scmp-seg JSR2 ,&continue JCN
				DUP2 ;build-module-photo JSR2
				;&limit LDA INC ;&limit STA
			&continue
			( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		&end
		POP2

RTN
	&limit $1

@build-module-calendar ( name* -- name* )

	;ul-tag ^<
		STH2
		;diary-path-pages/end ;diary-path-pages
		&loop
			LDA2k ;load/diary JSR2 
			STH2kr ,&append-li JSR
			INC2 INC2 GTH2k ,&loop JCN
		POP2 POP2
		STH2r
	;ul-tag ^>

	RTN

	&append-li ( name* -- )
		STH2
		DIARY 
		&while
			DUP2 #0005 ++ LDA [ LIT '+ ] ! ,&continue JCN
				DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 #01 ! ;&continue JCN2
				;li-tag ^<
					DUP2 ;build-module-event JSR2
				;li-tag ^>
				&continue
			( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		POP2 POP2r

RTN

@build-module-album ( name* -- name* )

	STH2
	;diary-path-pages/end ;diary-path-pages
	&loop
		LDA2k ;load/diary JSR2 
		STH2kr ,&append-figure JSR
		INC2 INC2 GTH2k ,&loop JCN
	POP2 POP2
	STH2r
	RTN

	&append-figure ( name* -- )

		STH2
		DIARY 
		&while
			DUP2 #0006 ++ ;no-picture SWP2 ;scmp-seg JSR2 ,&continue JCN
			DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 #01 ! ,&continue JCN
			DUP2 #0006 ++ ;term/pict SWP2 ;scmp-seg JSR2 ,&continue JCN
				DUP2 ;build-module-photo JSR2
			&continue
			( goto eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		POP2 POP2r

RTN

@build-module-portal ( name* -- name* )

	( stash depth ) DUP2 GET-DEPTH STH
	( start after ) DUP2 ;slen JSR2 ++ INC2 
	&while
		( stop at sibling ) LDAk ;chex JSR2 STHkr = ,&end JCN
		( when depth+1 ) LDAk ;chex JSR2 STHkr INC NEQ ,&continue JCN
			DUP2 2++ ;find-diary JSR2 DUP2 #0000 == ,&no-diary JCN
				DUP2 ;build-module-photo JSR2
				&no-diary
				POP2
			&continue
		( move to eol ) 2++ &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	&end
	POP2 POPr
	;ul-tag ^>

RTN

@build-module-archive ( name* -- name* )

	( stash depth ) DUP2 GET-DEPTH STH
	( start after ) DUP2 ;slen JSR2 ++ INC2 
	&while
		( stop at sibling ) LDAk ;chex JSR2 STHkr = ,&end JCN
		( when depth+1 ) LDAk ;chex JSR2 STHkr INC NEQ ,&continue JCN 
			2++ 
				;build-page-main-body JSR2
			&continue
		( move to eol ) 2++ &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	&end
	POP2 POPr

RTN

@build-module-event ( diary* -- )

	( event date )
	( clip bound ) DUP2 #0005 ++ STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;code-tag ^<>
	( clip restore ) [ LIT '+ ] STH2r STA
	( event spacer ) ^space ;mdash-entity-txt ^s ^space 
	( event name ) DUP2 #09 ;next-char JSR2 INC2 #09 ;next-char JSR2 INC2 ^s

RTN

@build-module-photo ( diary* -- )
 
	( html )
	;figure-tag ^<
		DUP2 ;to-diary-path JSR2 OVR2 #09 ;next-char JSR2 INC2 #09 ;next-char JSR2 INC2 ;add-img-tag JSR2
		;figcaption-tag ^<
			DUP2 #000a ++ ;term/name SWP2 ;scmp-seg JSR2 ,&skip JCN
				DUP2 ;extract-term JSR2 ;add-local JSR2 
				^space ;mdash-entity-txt ^s ^space 
				&skip
			#09 ;next-char JSR2 INC2 #09 ;next-char JSR2 INC2 ^s 
		;figcaption-tag ^>
	;figure-tag ^>

RTN

@extract-term ( diary* -- term* )

	#00 ;&buff STA
	#000a ++ 
	( clip bound ) DUP2 #09 ;next-char JSR2 STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;&buff ;scat JSR2
	( clip restore ) #09 STH2r STA
	;&buff

RTN
	&buff $16

@build-page-footer ( name* -- name* )

	;meta-footer-path .File/name DEO2
	#0300 .File/length DEO2
	FILE DUP2 ;slen JSR2 ++ STH2k .File/load DEO2
	( close ) #00 STH2r .File/success DEI2 ++ STA

RTN

@check-page ( page* -- )

	( look for redlinks )
	&while
		DUP2 ;href-attr SWP2 ;scmp-seg JSR2 #00 = ,&continue JCN
			( move to href ) #0006 ++ 
			( strip relative ) LDAk [ LIT '. ] = ,&continue JCN
			( strip jumps ) LDAk [ LIT '# ] = ,&continue JCN
			( strip http* ) DUP2 ;http-txt SWP2 ;scmp-seg JSR2 ,&continue JCN
			( filter ) DUP2 
			;&buff #0030 ;mcpy JSR2
			;&buff [ LIT '. ] #00 ;cswp JSR2
			;&buff [ LIT '_ ] #20 ;cswp JSR2
			( find term )
			;&buff ;find-term JSR2 #0000 !! ,&continue JCN
				;&buff ;error-broken-link ERROR
			&continue
		INC2 LDAk ,&while JCN
	POP2
RTN
	&buff $30

( tools )

@find-term ( name* -- term* )

	STH2
	LEXICON 
	&while
		( move to name ) 2++ 
		( test ) DUP2 STH2kr ;scmp JSR2 ,&end JCN
		( move to eol ) &eol INC2 LDAk ,&eol JCN INC2
		LDAk ,&while JCN
	POP2 #0000
	&end
	POP2r

RTN

@find-diary ( name* -- diary* )

	( load page2 )
	;diary-path-page2 ;load/diary JSR2
	;&search JSR2 
	( load page1 )
	DUP2 #0000 !! ,&page1 JCN
		POP2 ;diary-path-page1 ;load/diary JSR2 
		DUP2 ;&search JSR2
		&page1
	( load page0 )
	DUP2 #0000 !! ,&page0 JCN
		POP2 ;diary-path-page0 ;load/diary JSR2 
		DUP2 ;&search JSR2
		&page0
	RTN

	&search ( name* -- diary* )
		STH2
		DIARY 
		&while
			( skip events ) DUP2 #0006 ++ ;no-picture SWP2 ;scmp-seg JSR2 ,&skip JCN
			( test name ) DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 ,&end JCN
			&skip
			( move to eol ) &eol INC2 LDAk ,&eol JCN INC2
			LDAk ,&while JCN
		POP2 #0000
		&end
		POP2r

RTN

@find-parent ( term* -- parent* )

	REWIND 
	( stash depth ) LDAk ;chex JSR2 #01 - STH
	LEXICON SWP2
	&loop
		1-- #00 ;prev-char JSR2 
		INC2k LDA ;chex JSR2 STHkr ! ,&continue JCN
			NIP2 POPr #0003 ++ RTN
			&continue
		LTH2k ,&loop JCN
	( pop depth ) POPr
	#0002

RTN

( Web framework )

@add-a-tag ( path* -- )

	STH2
	[ LIT '< ] ^c
	[ LIT 'a ] ^c
	STH2r ;href-attr ;add-attr JSR2
	[ LIT '> ] ^c

RTN

@add-img-tag ( src* alt* -- )

	STH2 STH2
	[ LIT '< ] ^c
	;img-tag ^s
	STH2r ;src-attr ;add-attr JSR2
	STH2r ;alt-attr ;add-attr JSR2
	[ LIT '/ ] ^c
	[ LIT '> ] ^c

RTN

@add-link-tag ( rel* type* href* -- )
	
	FILE RESUME
		[ LIT '< ] PUTC 
		;link-tag PUTS CLOSE
			;href-attr ;add-attr JSR2
			;type-attr ;add-attr JSR2
			;rel-attr ;add-attr JSR2
	FILE RESUME 
		[ LIT '/ ] PUTC
		[ LIT '> ] PUTC
		CLOSE

RTN

@add-attr ( body* attr* -- )
 	
 	^space
	^s
	[ LIT '= ] ^c
	[ LIT '" ] ^c
	^s
	[ LIT '" ] ^c

RTN

@add-local ( name* -- )

	DUP2 ;term/name ;scmp JSR2 ,&no-link JCN

	FILE RESUME
		SWP2 ;thread-local JSR2
		CLOSE

	RTN
	&no-link
		^s [ LIT '/ ] ^c

RTN

( fast thread )

@thread-attr ( thread* name* body* -- thread* )

	STH2 STH2
	#20 PUTC
	STH2r PUTS
	LIT '= PUTC
	LIT '" PUTC
	STH2r PUTS
	LIT '" PUTC

RTN

@thread-local ( thread* name* -- thread* )

	STH2
	LIT '< PUTC
	LIT 'a PUTC		
	;href-attr STH2kr ;to-filename JSR2 ;thread-attr JSR2
	LIT '> PUTC
	STH2r PUTS
	LIT '< PUTC
	LIT '/ PUTC
	LIT 'a PUTC
	LIT '> PUTC

RTN

@to-filename ( name* -- filename* )

	;buff
		SWP2 PUTS
		;html-ext PUTS
		CLOSE
	;buff DUP2 #20 [ LIT '_ ] ;cswp JSR2

RTN

@to-output-path ( name* -- filepath* )
	
	;buff
		;output-path PUTS
		SWP2 PUTS
		;html-ext PUTS
		CLOSE
	;buff DUP2 #20 [ LIT '_ ] ;cswp JSR2

RTN

@to-include-path ( name* -- filepath* )

	;buff
		;input-path PUTS
		SWP2 PUTS
		;htm-ext PUTS
		CLOSE
	;buff DUP2 #20 [ LIT '_ ] ;cswp JSR2

RTN

@to-diary-path ( diary* -- filepath* )
	
	#0006 ++
	#00 ;buff STA
	;media-diary-path ;buff ;scpy JSR2
	( clip bound ) DUP2 #0003 ++ STH2
	( clip start ) #00 STH2kr STA
	( clip wrap ) ;buff ;scat JSR2
	( clip restore ) #09 STH2r STA
	;jpg-ext ;buff ;scat JSR2
	;buff

RTN

( generics )

@prev-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr = ,&end JCN
		LDAk #00 = ,&end JCN
		1-- LDAk ,&while JCN
	&end
		POPr

RTN

@next-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr = ,&end JCN
		LDAk #00 = ,&end JCN
		INC2 LDAk ,&while JCN
	&end
		POPr

RTN

( helpers )

@cswp ( string* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

RTN

@ccat ( char dst* -- )
	
	1--
	&while
		INC2 LDAk ,&while JCN
	STH2k STA
	#00 STH2r INC2 STA

RTN

@chex ( char -- hex )
	
	DUPk #2f GTH SWP #3a LTH #0101 == ,&num JCN
	DUPk #40 GTH SWP #47 LTH #0101 == ,&uca JCN
	DUPk #60 GTH SWP #67 LTH #0101 == ,&lca JCN
	POP #00 RTN
	&num ( char -- hex )
		LIT '0 - RTN
	&uca ( char -- hex )
		LIT 'A - #0a + RTN
	&lca ( char -- hex )
		LIT 'a - #0a + RTN

RTN

@scmp ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		LDAk LDAkr STHr #0000 !! ,&not-end JCN
			POP2 POP2r #01 RTN
			&not-end
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scmp-seg ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		INC2k LDA #00 ! ,&continue JCN
			POP2 POP2r #01 RTN
			&continue
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scat ( src* dst* -- )

	DUP2 ,slen JSR ++ ,scpy JSR

RTN

@scpy ( src* dst* -- )

	STH2 
	DUP2
	&while
		SWP2k -- STH2kr ++ STH2
		LDAk STH2r STA
		INC2 LDAk ,&while JCN
	SWP2 -- STH2r ++ 
	#00 ROT ROT STA

RTN

@slen ( addr* -- length* )

	1-- DUP2
	&while
		INC2 LDAk ,&while JCN
	SWP2 --
	1--

RTN

@mclr ( addr* length* -- )
	
	OVR2 ++ SWP2
	&loop
		DUP2 #00 ROT ROT STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@mcpy ( a* b* length* -- )
	
	STH2
	,&b STR2 ,&a STR2
	STH2r #0000
	&loop
		STH2k
		,&a LDR2 STH2kr ++ LDA 
		,&b LDR2 STH2r ++ STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN
	&a $2 &b $2

@scap ( addr* -- addr* )

	1-- &eol INC2 LDAk ,&eol JCN

RTN

@put ( addr* string* -- addr* )

	&string ( addr* string* -- addr* )
		&while
			SWP2 STH2
			LDAk STH2r ROT
			,&char JSR 
			SWP2 INC2 
			LDAk ,&while JCN
		POP2
	RTN
	&char ( addr* char -- addr* )
		ROT ROT STH2k STA
		STH2r INC2
	RTN
	
RTN

@print-hex ( value* -- )
	
	&short ( value* -- )
		SWP ,&echo JSR 
	&byte ( value -- )
		,&echo JSR
	RTN

	&echo ( value -- )
	STHk #04 SFT ,&parse JSR .Console/write DEO
	STHr #0f AND ,&parse JSR .Console/write DEO
	RTN
	&parse ( value -- char )
		DUP #09 GTH ,&above JCN #30 + RTN &above #09 - #60 + RTN

RTN

@print-string ( addr* -- )

	1-- 
	&loop 
		INC2 LDAk [ DUP .Console/write DEO ] ,&loop JCN 
	POP2

RTN

@html-ext ".html $1
@htm-ext ".htm $1
@jpg-ext ".jpg $1

@doctype "<!DOCTYPE 20 "html> $1
@html-tag "html $1
@head-tag "head $1
@title-tag "title $1
@body-tag "body $1
@header-tag "header $1
@nav-tag "nav $1
@main-tag "main $1
@ul-tag "ul $1
@li-tag "li $1
@a-tag "a $1
@img-tag "img $1
@code-tag "code $1
@link-tag "link $1
@figure-tag "figure $1
@figcaption-tag "figcaption $1

@src-attr "src $1
@alt-attr "alt $1
@href-attr "href $1
@rel-attr "rel $1
@type-attr "type $1

@title-txt "XXIIVV $1
@stylesheet-txt "stylesheet $1
@shortcuticon-txt "shortcut 20 "icon $1
@mdash-entity-txt "&mdash; $1
@http-txt "http $1
@index-txt "index $1
@calendar-txt "calendar $1
@journal-txt "journal $1

@lexicon-path "database/lexicon $1
@diary-path-page2 "database/diary2 $1
@diary-path-page1 "database/diary1 $1
@diary-path-page0 "database/diary0 $1
@input-path "content/ $1
@output-path "../site/ $1
@home-path "home.html $1
@meta-footer-path "content/meta_footer.htm $1
@stylesheet-path "../links/main.css $1
@logo-path "../media/icon/logo.svg $1
@media-diary-path "../media/diary/ $1
@shortcuticon-path "../media/services/icon.png $1

@no-picture "000 $1

@error-include-size "--include_size_exceeded $1
@error-include-missing "include_missing $1
@error-broken-link "--redlink $1
@error-term-name-size "--term_name_size_exceeded $1
@error-output-size "--output_size_exceeded $1

@stylesheet-type "text/css $1
@shortcuticon-type "image/png $1

@diary-path-pages 
	:diary-path-page2
	:diary-path-page1
	:diary-path-page0
	&end

|1800