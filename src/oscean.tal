( The Bequest Globe )

%+  { ADD } %-   { SUB }              %/   { DIV }  
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ } 
%++ { ADD2 } %-- { SUB2 }              %// { DIV2 } 
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }  

%DEBUG  { ;print-hex JSR2 #0a .Console/write DEO }
%DEBUG2 { SWP ;print-hex JSR2 ;print-hex JSR2 #0a .Console/write DEO }

%RTN { JMP2r }
%HALT { #0101 .System/debug DEO2 }

%LEXICON { #1800 }
%LEXICON-SIZE { #1800 }
%DIARY { #3000 }
%DIARY-SIZE { #5000 }
%INCLUDE-SIZE { #7000 }

( Map:
	0x1800 - 0x3000 Lexicon
	0x3000 - 0x8000 Diary
	0x8000 - 0xffff Output
 )

%FILE { #8000 }

%^s { FILE ;scat JSR2 }
%^c { FILE ;ccat JSR2 }
%^space { #20 ^c }
%^open { STH2 LIT '< ^c STH2r ^s LIT '> ^c }
%^close { STH2 LIT '< ^c LIT '/ ^c STH2r ^s LIT '> ^c }
%^wrap { STH2k ^open ^s STH2r ^close }

%GET-DEPTH { #0002 -- LDA ;chex JSR2 }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset-hs $2 &offset-ls $2 &name   $2 &length $2 &load $2 &save   $2 ]
|b0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000

( program )

|0100 ( -> )

	;load JSR2
	
	;build JSR2

	( ;focus-page ;find-term JSR2 ;build-page JSR2 )

	HALT
	
BRK

@focus-page "talk $1

@load ( -- )
	
	;lexicon-path ,&lexicon JSR
	RTN
	&lexicon ( path* -- )
		( load lexicon )
		.File/name DEO2
		LEXICON-SIZE  .File/length DEO2
		LEXICON       .File/load DEO2
		( remove linebreaks ) LEXICON #0a #00 ;cswp JSR2 
	RTN
	&diary ( path* -- )
		DIARY DIARY-SIZE ;mclr JSR2
		( load diary )
		.File/name DEO2
		DIARY-SIZE  .File/length DEO2
		DIARY       .File/load DEO2
		( remove linebreaks ) DIARY #0a #00 ;cswp JSR2 
	RTN

RTN

@build ( -- )
	
	#0000
	LEXICON
	&while
		( move to name ) #0002 ++ 
		( display )
		( print count ) OVR2 SWP ;print-hex JSR2 ;print-hex JSR2 #0d .Console/write DEO
		( incr count ) SWP2 INC2 SWP2
		( build ) DUP2 ;build-page JSR2
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2
	#0a .Console/write DEO 

RTN

@print-error ( name* msg* -- )
	
	SWP2 
	#0001 --
	&name-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&name-loop JCN
	POP2
	#20 .Console/write DEO 
	#0001 --
	&msg-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&msg-loop JCN
	POP2
	#0a .Console/write DEO 

RTN

( ---------------------------------- )

@build-page ( name* -- )

	( keep a copy )
	#00 ;&current STA
	DUP2 ;&current ;scpy JSR2

	( create file )
	#00 FILE STA

	;doctype ^s
	( create body )
	;html-tag ^open
		( head )
		;build-page-head JSR2
		( body )
		;body-tag ^open
			;build-page-header JSR2
			;build-page-nav JSR2
			;build-page-main JSR2
			;build-page-footer JSR2
		;body-tag ^close
	;html-tag ^close

	( save file )
	;to-filepath JSR2 .File/name DEO2
	FILE ;slen JSR2   .File/length DEO2
	FILE              .File/save DEO2

RTN
	&current $20

@build-page-head ( name* -- name* )

	;head-tag ^open
		;stylesheet-txt ;stylesheet-type ;stylesheet-path ;add-link-tag JSR2
		;shortcuticon-txt ;shortcuticon-type ;shortcuticon-path ;add-link-tag JSR2
		;title-tag ^open
			;title ^s
			^space
			;mdash-entity-txt ^s
			^space
			DUP2 ^s
		;title-tag ^close
	;head-tag ^close

RTN

@build-page-header ( name* -- name* )

	;header-tag ^open
		;home-path ;add-a-tag JSR2
			;logo-path ;logo-alt-txt ;add-img-tag JSR2
		;a-tag ^close
	;header-tag ^close

RTN

@build-page-nav ( name* -- name* )

	;nav-tag ^open
		DUP2 GET-DEPTH #00 = ,&no-siblings JCN
		( parents ) 
		DUP2 GET-DEPTH #02 < ,&no-parents JCN
			DUP2 ;find-parent JSR2 ;find-parent JSR2 ,&children JSR POP2
			&no-parents
		( siblings ) 
		DUP2 GET-DEPTH #01 < ,&no-siblings JCN
			DUP2 ;find-parent JSR2 ,&children JSR POP2
			&no-siblings
		( children ) ,&children JSR
	;nav-tag ^close
	RTN

	&children ( name* -- name* )

		;ul-tag ^open
		DUP2
		( stash depth ) DUP2 GET-DEPTH STH
		( start after ) DUP2 ;slen JSR2 ++ INC2 
		&while
			( stop at sibling ) LDAk ;chex JSR2 STHkr = ,&end JCN
			( when depth+1 ) LDAk ;chex JSR2 STHkr INC NEQ ,&continue JCN
				;li-tag ^open
				DUP2 #0002 ++ ;add-local JSR2
				;li-tag ^close
				&continue
			( move to eol ) #0002 ++ DUP2 ;slen JSR2 ++ INC2 
			LDAk ,&while JCN
		&end
		POP2 POPr
		;ul-tag ^close

RTN

@build-page-main ( name* -- name* )
	
	;main-tag ^open

		( display picture )
		;build-page-main-pict JSR2
		;build-page-main-body JSR2
		;build-page-main-events JSR2

	;main-tag ^close

RTN

@build-page-main-body ( name* -- name* )

	( create include path )
	#00 ;buff STA
	;input-path ;buff ;scpy JSR2
	DUP2 ;buff ;scat JSR2
	;htm-ext ;buff ;scat JSR2
	;buff #20 LIT '_ ;cswp JSR2

	( find include position )
	FILE DUP2 ;slen JSR2 ++ STH2

	( write body )
	;buff .File/name DEO2
	INCLUDE-SIZE .File/length DEO2
	STH2kr .File/load DEO2

	.File/success DEI2 INCLUDE-SIZE !! ,&continue JCN
		DUP2 ;error-include-size ;print-error JSR2
		&continue

	( close )
	#00 STH2r .File/success DEI2 ++ STA

RTN

@build-page-main-pict ( name* -- name* )
	
	( load page2 )
	;diary-path-page2 ;load/diary JSR2
	DUP2 ;find-diary JSR2 

	( load page1 )
	DUP2 #0000 !! ,&page1 JCN
		POP2 ;diary-path-page1 ;load/diary JSR2
		DUP2 ;find-diary JSR2
		&page1

	( load page0 )
	DUP2 #0000 !! ,&page0 JCN
		POP2 ;diary-path-page0 ;load/diary JSR2
		DUP2 ;find-diary JSR2
		&page0

	( skip when no diaries )
	DUP2 #0000 !! ,&continue JCN
		POP2 RTN
		&continue

	( rewind ) #00 ;prev-char JSR2 INC2 
	STH2

	( create src* )
	#00 ;buff STA
	;media-diary-path ;buff ;scpy JSR2
	STH2kr #0006 ++ LDAk ;buff ;ccat JSR2
	INC2 LDAk ;buff ;ccat JSR2
	INC2 LDA ;buff ;ccat JSR2
	;jpg-ext ;buff ;scat JSR2

	( html )
	;figure-tag ^open
		;buff STH2kr #000a ++ ;add-img-tag JSR2
		;figcaption-tag ^open
		DUP2 ;slen JSR2 STH2kr ++ #000a ++ ^s
		;figcaption-tag ^close
	;figure-tag ^close
	POP2r

RTN

@build-page-main-events ( name* -- name* )

	;ul-tag ^open

	;diary-path-page2 ;load/diary JSR2
	;draw-events JSR2
	;diary-path-page1 ;load/diary JSR2
	;draw-events JSR2
	;diary-path-page0 ;load/diary JSR2
	;draw-events JSR2
	
	;ul-tag ^close

RTN

@draw-events ( name* -- name* )

	STH2
	DIARY 
	&while
		DUP2 #0005 ++ LDA LIT '+ ! ,&continue JCN 
			DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 #01 ! ,&continue JCN
			( move to event name )
			DUP2 #000a ++ STH2kr ;slen JSR2 ++ INC2 ;li-tag ^wrap
			&continue
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ;&while JCN2
	&end
	POP2 STH2r

RTN

@build-page-footer ( name* -- name* )

	;footer-tag ^open
		;creativecommon-path ;add-a-external-tag JSR2
			;creativecommon-icon-path ;creativecommon-alt-txt ;add-img-tag JSR2
		;a-tag ^close
		;webring-path ;add-a-external-tag JSR2
			;webring-icon-path ;webring-alt-txt ;add-img-tag JSR2
		;a-tag ^close
		;merveilles-path ;add-a-external-tag JSR2
			;merveilles-icon-path ;merveilles-alt-txt ;add-img-tag JSR2
		;a-tag ^close
		;span-tag ^open
			;devinelulinvega-path ;add-a-tag JSR2
				;devinelulinvega-txt ^s
			;a-tag ^close
			^space
			;copy-entity-txt ^s
			^space
			;year-txt ^s
			^space
			;mdash-entity-txt ^s
			^space
			;about-path ;add-a-tag JSR2
				;license-txt ^s
			;a-tag ^close
		;span-tag ^close
	;footer-tag ^close

RTN

( tools )

@find-term ( name* -- term* )

	STH2
	LEXICON 
	&while
		( move to name ) #0002 ++ 
		( test ) DUP2 STH2kr ;scmp JSR2 ,&end JCN
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	&end
	POP2r

RTN

@find-diary ( name* -- diary* )

	STH2
	DIARY 
	&while
		( skip events ) DUP2 #0006 ++ ;&blank SWP2 ;scmp-seg JSR2 ,&skip JCN
		( move to name ) #000a ++
		( test ) DUP2 STH2kr SWP2 ;scmp-seg JSR2 ,&end JCN
		&skip
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2 #0000
	&end
	POP2r

RTN
	&blank "000 $1

@find-parent ( term* -- parent* )

	( rewind line )
	#00 ;prev-char JSR2 

	( stash depth ) INC2k LDA ;chex JSR2 #01 - STH

	LEXICON SWP2
	&loop
		#0001 -- #00 ;prev-char JSR2 
		INC2k LDA ;chex JSR2 STHkr ! ,&continue JCN
			NIP2 POPr #0003 ++ RTN
			&continue
		LTH2k ,&loop JCN
	POPr
	#0002

RTN

( Web framework )

@add-a-tag ( path* -- )

	STH2
	LIT '< ^c
	LIT 'a ^c
	STH2r ;href-attr ;add-attr JSR2
	LIT '> ^c

RTN

@add-a-external-tag ( path* -- )

	STH2
	LIT '< ^c
	LIT 'a ^c
	STH2r ;href-attr ;add-attr JSR2
	;blank-target ;target-attr ;add-attr JSR2
	LIT '> ^c

RTN

@add-img-tag ( src* alt* -- )
	
	STH2 STH2
	LIT '< ^c
	;img-tag ^s
	STH2r ;src-attr ;add-attr JSR2
	STH2r ;alt-attr ;add-attr JSR2
	LIT '/ ^c
	LIT '> ^c

RTN

@add-link-tag ( rel* type* href* -- )
	
	LIT '< ^c
	;link-tag ^s
		;href-attr ;add-attr JSR2
		;type-attr ;add-attr JSR2
		;rel-attr ;add-attr JSR2
	LIT '/ ^c
	LIT '> ^c

RTN

@add-attr ( attr* body* -- )
 	
 	^space
	^s
	LIT '= ^c
	LIT '" ^c
	^s
	LIT '" ^c

RTN

@add-local ( name* -- )

	DUP2 ;build-page/current ;scmp JSR2 ,&no-link JCN
	DUP2 ;to-filepath JSR2 ;add-a-tag JSR2 ^s ;a-tag ^close
	RTN
	&no-link
	^s LIT '/ ^c

RTN

@to-filepath ( name* -- filepath* )
	
	#00 ;buff STA
	;output-path ;buff ;scpy JSR2
	( name* ) ;buff ;scat JSR2
	;html-ext ;buff ;scat JSR2
	;buff #20 LIT '_ ;cswp JSR2
	;buff

RTN

( generics )

@cswp ( string* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

RTN

@prev-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr = ,&end JCN
		LDAk #00 = ,&end JCN
		#0001 -- LDAk ,&while JCN
	&end
		POPr

RTN

( helpers )

@ccat ( char dst* -- )
	
	#0001 --
	&while
		INC2 LDAk ,&while JCN
	STH2k STA
	#00 STH2r INC2 STA

RTN

@chex ( char -- hex )
	
	DUPk #2f GTH SWP #3a LTH #0101 == ,&num JCN
	DUPk #40 GTH SWP #47 LTH #0101 == ,&uca JCN
	DUPk #60 GTH SWP #67 LTH #0101 == ,&lca JCN
	POP #00 RTN
	&num ( char -- hex )
		LIT '0 - RTN
	&uca ( char -- hex )
		LIT 'A - #0a + RTN
	&lca ( char -- hex )
		LIT 'a - #0a + RTN

RTN

@scmp ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		LDAk LDAkr STHr #0000 !! ,&not-end JCN
			POP2 POP2r #01 RTN
			&not-end
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scmp-seg ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		INC2k LDA #00 ! ,&continue JCN
			POP2 POP2r #01 RTN
			&continue
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scat ( src* dst* -- )

	DUP2 ,slen JSR ++ ,scpy JSR

RTN

@scpy ( src* dst* -- )

	STH2 
	DUP2
	&while
		SWP2k -- STH2kr ADD2 STH2
		LDAk STH2r STA
		INC2 LDAk ,&while JCN
	SWP2 -- STH2r ADD2 
	#00 ROT ROT STA

RTN

@slen ( addr* -- length* )

	#0001 -- DUP2
	&while
		INC2 LDAk ,&while JCN
	SWP2 --
	#0001 --

RTN

@mclr ( addr* length* -- )
	
	OVR2 ADD2 SWP2
	&loop
		DUP2 #00 ROT ROT STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@print-hex ( value* -- )
	
	STHk #04 SFT ,&parse JSR .Console/write DEO
	STHr #0f AND ,&parse JSR .Console/write DEO
	RTN
	&parse ( value -- char )
		DUP #09 GTH ,&above JCN #30 ADD RTN &above #09 SUB #60 ADD RTN

RTN

@print-string ( addr* -- )

	#0001 --
	&loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&loop JCN
	POP2
	( linebreak ) #0a .Console/write DEO 

RTN

@html-ext ".html $1
@htm-ext ".htm $1
@jpg-ext ".jpg $1

@title "XXIIVV $1

@doctype "<!DOCTYPE 20 "html> $1
@html-tag "html $1
@head-tag "head $1
@title-tag "title $1
@body-tag "body $1
@header-tag "header $1
@nav-tag "nav $1
@main-tag "main $1
@footer-tag "footer $1
@img-tag "img $1
@a-tag "a $1
@span-tag "span $1
@link-tag "link $1
@ul-tag "ul $1
@li-tag "li $1
@figure-tag "figure $1
@figcaption-tag "figcaption $1

@target-attr "target $1
@src-attr "src $1
@alt-attr "alt $1
@href-attr "href $1
@blank-target "_blank $1
@rel-attr "rel $1
@type-attr "type $1

@logo-alt-txt "XXIIVV $1
@year-txt "2021 $1
@stylesheet-txt "stylesheet $1
@shortcuticon-txt "shortcut 20 "icon $1
@license-txt "BY-NC-SA 20 "4.0 $1
@copy-entity-txt "&copy; $1
@mdash-entity-txt "&mdash; $1
@devinelulinvega-txt "Devine 20 "Lu 20 "Linvega $1
@creativecommon-alt-txt "CreativeCommons $1
@webring-alt-txt "Webring $1
@merveilles-alt-txt "Merveilles $1

@lexicon-path "database/lexicon $1
@diary-path-page2 "database/diary2 $1
@diary-path-page1 "database/diary1 $1
@diary-path-page0 "database/diary0 $1
@input-path "content/ $1
@output-path "../site/ $1
@home-path "home.html $1
@logo-path "../media/icon/logo.svg $1
@media-diary-path "../media/diary/ $1
@stylesheet-path "../links/main.css $1
@shortcuticon-path "../media/services/icon.png $1
@about-path "about.html $1
@devinelulinvega-path "devine_lu_linvega.html $1
@creativecommon-path "https://creativecommons.org/licenses/by-nc-sa/4.0 $1
@creativecommon-icon-path "../media/icon/cc.svg $1
@webring-path "http://webring.xxiivv.com/ $1
@webring-icon-path "../media/icon/webring.svg $1
@merveilles-path "https://merveilles.town/@neauoire $1
@merveilles-icon-path "../media/icon/merveilles.svg $1

@error-include-size "--include_size_exceeded $1

@stylesheet-type "text/css $1
@shortcuticon-type "image/png $1

@buff $100

|1800