( The Bequest Globe )

%+  { ADD } %-   { SUB }              %/   { DIV }  
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ } 
%++ { ADD2 } %-- { SUB2 }              %// { DIV2 } 
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }  

%1-- { #0001 -- }
%2++ { INC2 INC2 }

%DEBUG  { ;print-hex/byte JSR2 #0a .Console/write DEO }
%DEBUG2 { ;print-hex/short JSR2 #0a .Console/write DEO }
%PRINT { ;print-string JSR2 #0a .Console/write DEO }

%RTN { JMP2r }
%HALT { #0101 .System/debug DEO2 }

( Map:
	0x1800 - 0x3000 Lexicon
	0x3000 - 0x8000 Diary
	0x8000 - 0xffff Output )

%LEXICON { #1800 }
%LEXICON-SIZE { #1800 }
%DIARY { #3000 }
%DIARY-SIZE { #5000 }
%INCLUDE-SIZE { #7000 }
%FILE { #8000 }
%FILE-SIZE { #8000 }

%^s { FILE ;scat JSR2 }
%^c { FILE ;ccat JSR2 }
%^space { #20 ^c }
%^< { STH2 LIT '< ^c STH2r ^s LIT '> ^c }
%^> { STH2 LIT '< ^c LIT '/ ^c STH2r ^s LIT '> ^c }
%^<> { STH2k ^< ^s STH2r ^> }

%GET-DEPTH { #0002 -- LDA ;chex JSR2 }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|a0 @File       [ &vector $2 &success  $2 &offset-hs $2 &offset-ls $2 &name   $2 &length $2 &load $2 &save   $2 ]
|b0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000 $2 

@term
	&name $20
	&pict $4

( program )

|0100 ( -> )

	;load JSR2
	,focus-page LDR ,build-one JCN
		,build-all JMP
	HALT
	
BRK

@focus-page ( "directory ) $1

@build-one ( -> ) 

	;focus-page ;find-term JSR2 ;build-page JSR2 
	HALT

BRK

@build-all ( -- )
	
	#0000
	LEXICON
	&while
		( move to name ) 2++ 
		( display )
			( print count ) OVR2 ;print-hex/short JSR2 #0d .Console/write DEO
			( incr count ) SWP2 INC2 SWP2
		( build ) DUP2 ;build-page JSR2
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2 POP2
	#0a .Console/write DEO 
	HALT

RTN

@load ( -- )
	
	;lexicon-path ,&lexicon JSR
	RTN
	&lexicon ( path* -- )
		( load lexicon )
		.File/name DEO2
		LEXICON-SIZE  .File/length DEO2
		LEXICON       .File/load DEO2
		( remove linebreaks ) LEXICON #0a #00 ;cswp JSR2 
	RTN
	&diary ( path* -- )
		DIARY DIARY-SIZE ;mclr JSR2
		( load diary )
		.File/name DEO2
		DIARY-SIZE  .File/length DEO2
		DIARY       .File/load DEO2
		( remove linebreaks ) DIARY #0a #00 ;cswp JSR2 
	RTN

RTN

@print-error ( name* msg* -- )
	
	SWP2 
	1--
	&name-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&name-loop JCN
	POP2
	#20 .Console/write DEO 
	1--
	&msg-loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&msg-loop JCN
	POP2
	#0a .Console/write DEO 

RTN

( ---------------------------------- )

@build-page ( name* -- )

	( keep a copy )
	#00 .term/name STZ
	DUP2 ;term/name ;scpy JSR2

	( create file )
	FILE FILE-SIZE ;mclr JSR2

	;doctype ^s
	( create body )
	;html-tag ^<
		( head )
		;head-tag ^<
			;stylesheet-txt ;stylesheet-type ;stylesheet-path ;add-link-tag JSR2
			;shortcuticon-txt ;shortcuticon-type ;shortcuticon-path ;add-link-tag JSR2
			;title-tag ^<
				;title-txt ^s ^space ;mdash-entity-txt ^s ^space DUP2 ^s
			;title-tag ^>
		;head-tag ^>
		( body )
		;body-tag ^<
			;build-page-header JSR2
			;build-page-nav JSR2
			;build-page-main JSR2
			;build-page-footer JSR2
		;body-tag ^>
	;html-tag ^>

	( save file )
	;to-filepath JSR2 .File/name DEO2
	FILE ;slen JSR2   .File/length DEO2
	FILE              .File/save DEO2

	FILE ;check-page JSR2

RTN

@build-page-header ( name* -- name* )

	;header-tag ^<
		;home-path ;add-a-tag JSR2
			;logo-path ;title-txt ;add-img-tag JSR2
		;a-tag ^>
	;header-tag ^>

RTN

@build-page-nav ( name* -- name* )

	;nav-tag ^<
		DUP2 GET-DEPTH #00 = ,&no-siblings JCN
		( parents ) 
		DUP2 GET-DEPTH #02 < ,&no-parents JCN
			DUP2 ;find-parent JSR2 ;find-parent JSR2 ,&children JSR POP2
			&no-parents
		( siblings ) 
		DUP2 GET-DEPTH #01 < ,&no-siblings JCN
			DUP2 ;find-parent JSR2 ,&children JSR POP2
			&no-siblings
		( children ) ,&children JSR
	;nav-tag ^>
	RTN

	&children ( name* -- name* )

		;ul-tag ^<
		DUP2
		( stash depth ) DUP2 GET-DEPTH STH
		( start after ) DUP2 ;slen JSR2 ++ INC2 
		&while
			( stop at sibling ) LDAk ;chex JSR2 STHkr = ,&end JCN
			( when depth+1 ) LDAk ;chex JSR2 STHkr INC NEQ ,&continue JCN
				;li-tag ^<
				DUP2 2++ ;add-local JSR2
				;li-tag ^>
				&continue
			( move to eol ) 2++ DUP2 ;slen JSR2 ++ INC2 
			LDAk ,&while JCN
		&end
		POP2 POPr
		;ul-tag ^>

RTN

@build-page-main ( name* -- name* )
	
	;main-tag ^<

		( display picture )
		;build-page-main-pict JSR2
		;build-page-main-body JSR2
		DUP2 1-- LDA [ LIT ', ] ! ,&no-album JCN 
			;build-module-album JSR2
			&no-album
		;build-module-calendar JSR2

	;main-tag ^>

RTN

@build-page-main-body ( name* -- name* )

	( create include path )
	#00 ;buff STA
	;input-path ;buff ;scpy JSR2
	DUP2 ;buff ;scat JSR2
	;htm-ext ;buff ;scat JSR2
	;buff #20 [ LIT '_ ] ;cswp JSR2

	( find include position )
	FILE DUP2 ;slen JSR2 ++ STH2

	( write body )
	;buff .File/name DEO2
	INCLUDE-SIZE .File/length DEO2
	STH2kr .File/load DEO2

	.File/success DEI2 INCLUDE-SIZE !! ,&continue JCN
		DUP2 ;error-include-size ;print-error JSR2
		&continue

	( close )
	#00 STH2r .File/success DEI2 ++ STA

RTN

@build-page-main-pict ( name* -- name* )
	
	( reset pict )
	#0000 .term/pict STZ2
	#00 .term/pict INC INC STZ

	( load page2 )
	;diary-path-page2 ;load/diary JSR2
	DUP2 ;find-diary JSR2 

	( load page1 )
	DUP2 #0000 !! ,&page1 JCN
		POP2 ;diary-path-page1 ;load/diary JSR2
		DUP2 ;find-diary JSR2
		&page1

	( load page0 )
	DUP2 #0000 !! ,&page0 JCN
		POP2 ;diary-path-page0 ;load/diary JSR2
		DUP2 ;find-diary JSR2
		&page0

	( skip when no diaries )
	DUP2 #0000 !! ,&continue JCN
		POP2 RTN
		&continue

	( rewind ) #00 ;prev-char JSR2 INC2 

	( save pict )
	DUP2 #0006 ++ LDAk ;term/pict ;ccat JSR2
	INC2 LDAk ;term/pict ;ccat JSR2
	INC2 LDA ;term/pict ;ccat JSR2

	;build-module-photo JSR2

RTN

@build-module-calendar ( name* -- name* )

	;ul-tag ^<
		;diary-path-page2 ;load/diary JSR2 
		,&append-li JSR
		;diary-path-page1 ;load/diary JSR2 
		,&append-li JSR
		;diary-path-page0 ;load/diary JSR2 
		,&append-li JSR
	;ul-tag ^>

	RTN

	&append-li ( name* -- name* )
		STH2
		DIARY 
		&while
			DUP2 #0005 ++ LDA [ LIT '+ ] ! ,&continue JCN 
				DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 #01 ! ,&continue JCN
				( move to event name )
				DUP2 #000a ++ STH2kr ;slen JSR2 ++ INC2 ;li-tag ^<>
				&continue
			( move to eol ) DUP2 ;slen JSR2 ++ INC2 
			LDAk ;&while JCN2
		POP2 STH2r

RTN

@build-module-album ( name* -- name* )

	;diary-path-page2 ;load/diary JSR2 
	,&append-figure JSR
	;diary-path-page1 ;load/diary JSR2 
	,&append-figure JSR
	;diary-path-page0 ;load/diary JSR2 
	,&append-figure JSR
	RTN

	&append-figure ( name* -- name* )
		STH2
		DIARY 
		&while
			DUP2 #0006 ++ ;no-picture SWP2 ;scmp-seg JSR2 ,&continue JCN
			DUP2 #000a ++ STH2kr SWP2 ;scmp-seg JSR2 #01 ! ,&continue JCN
			DUP2 #0006 ++ ;term/pict SWP2 ;scmp-seg JSR2 ,&continue JCN
				DUP2 ;build-module-photo JSR2
			&continue
			( move to eol ) DUP2 ;slen JSR2 ++ INC2 
			LDAk ;&while JCN2
		POP2 STH2r

RTN

@build-module-photo ( diary* -- )

	( create src* )
	#00 ;buff STA
	;media-diary-path ;buff ;scpy JSR2
	DUP2 #0006 ++ LDAk ;buff ;ccat JSR2
	INC2 LDAk ;buff ;ccat JSR2
	INC2 LDA ;buff ;ccat JSR2
	;jpg-ext ;buff ;scat JSR2

	( move to title ) #09 ;next-char JSR2 INC2 #09 ;next-char JSR2 INC2 DUP2

	( html )
	;figure-tag ^<
		;buff SWP2 ;add-img-tag JSR2
		;figcaption-tag ^<
		^s
		;figcaption-tag ^>
	;figure-tag ^>

RTN

@build-page-footer ( name* -- name* )

	;footer-tag ^<
		;creativecommon-path ;add-a-external-tag JSR2
			;creativecommon-icon-path ;creativecommon-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;webring-path ;add-a-external-tag JSR2
			;webring-icon-path ;webring-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;merveilles-path ;add-a-external-tag JSR2
			;merveilles-icon-path ;merveilles-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;uxn-path ;add-a-tag JSR2
			;uxn-icon-path ;uxn-alt-txt ;add-img-tag JSR2
		;a-tag ^>
		;span-tag ^<
			;devinelulinvega-path ;add-a-tag JSR2
				;devinelulinvega-txt ^s
			;a-tag ^>
			^space
			;copy-entity-txt ^s
			^space
			;year-txt ^s
			^space
			;mdash-entity-txt ^s
			^space
			;about-path ;add-a-tag JSR2
				;license-txt ^s
			;a-tag ^>
		;span-tag ^>
	;footer-tag ^>

RTN

@check-page ( page* -- )

	( look for redlinks )
	&while
		DUP2 ;href-attr SWP2 ;scmp-seg JSR2 #00 = ,&continue JCN
			( move to href ) #0006 ++ 
			( strip relative ) LDAk [ LIT '. ] = ,&continue JCN
			( strip jumps ) LDAk [ LIT '# ] = ,&continue JCN
			( strip http* ) DUP2 ;http-txt SWP2 ;scmp-seg JSR2 ,&continue JCN
			( filter ) DUP2 
			;&buff #0030 ;mcpy JSR2
			;&buff [ LIT '. ] #00 ;cswp JSR2
			;&buff [ LIT '_ ] #20 ;cswp JSR2
			( find term )
			;&buff ;find-term JSR2 #0000 !! ,&continue JCN
				;&buff ;error-broken-link ;print-error JSR2
			&continue
		INC2 LDAk ,&while JCN
	POP2
RTN
	&buff $30

( tools )

@find-term ( name* -- term* )

	STH2
	LEXICON 
	&while
		( move to name ) 2++ 
		( test ) DUP2 STH2kr ;scmp JSR2 ,&end JCN
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2 #0000
	&end
	POP2r

RTN

@find-diary ( name* -- diary* )

	STH2
	DIARY 
	&while
		( skip events ) DUP2 #0006 ++ ;no-picture SWP2 ;scmp-seg JSR2 ,&skip JCN
		( move to name ) #000a ++
		( test ) DUP2 STH2kr SWP2 ;scmp-seg JSR2 ,&end JCN
		&skip
		( move to eol ) DUP2 ;slen JSR2 ++ INC2 
		LDAk ,&while JCN
	POP2 #0000
	&end
	POP2r

RTN

@find-parent ( term* -- parent* )

	( rewind line ) #00 ;prev-char JSR2 
	( stash depth ) INC2k LDA ;chex JSR2 #01 - STH
	LEXICON SWP2
	&loop
		1-- #00 ;prev-char JSR2 
		INC2k LDA ;chex JSR2 STHkr ! ,&continue JCN
			NIP2 POPr #0003 ++ RTN
			&continue
		LTH2k ,&loop JCN
	( pop depth ) POPr
	#0002

RTN

( Web framework )

@add-a-tag ( path* -- )

	STH2
	[ LIT '< ] ^c
	[ LIT 'a ] ^c
	STH2r ;href-attr ;add-attr JSR2
	[ LIT '> ] ^c

RTN

@add-a-external-tag ( path* -- )

	STH2
	[ LIT '< ] ^c
	[ LIT 'a ] ^c
	STH2r ;href-attr ;add-attr JSR2
	;blank-target ;target-attr ;add-attr JSR2
	[ LIT '> ] ^c

RTN

@add-img-tag ( src* alt* -- )
	
	STH2 STH2
	[ LIT '< ] ^c
	;img-tag ^s
	STH2r ;src-attr ;add-attr JSR2
	STH2r ;alt-attr ;add-attr JSR2
	[ LIT '/ ] ^c
	[ LIT '> ] ^c

RTN

@add-link-tag ( rel* type* href* -- )
	
	[ LIT '< ] ^c
	;link-tag ^s
		;href-attr ;add-attr JSR2
		;type-attr ;add-attr JSR2
		;rel-attr ;add-attr JSR2
	[ LIT '/ ] ^c
	[ LIT '> ] ^c

RTN

@add-attr ( attr* body* -- )
 	
 	^space
	^s
	[ LIT '= ] ^c
	[ LIT '" ] ^c
	^s
	[ LIT '" ] ^c

RTN

@add-local ( name* -- )

	DUP2 ;term/name ;scmp JSR2 ,&no-link JCN
	DUP2 ;to-filepath JSR2 ;add-a-tag JSR2 ^s ;a-tag ^>
	RTN
	&no-link
		^s [ LIT '/ ] ^c

RTN

@to-filepath ( name* -- filepath* )
	
	#00 ;buff STA
	;output-path ;buff ;scpy JSR2
	( name* ) ;buff ;scat JSR2
	;html-ext ;buff ;scat JSR2
	;buff #20 [ LIT '_ ] ;cswp JSR2
	;buff

RTN

( generics )

@prev-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr = ,&end JCN
		LDAk #00 = ,&end JCN
		1-- LDAk ,&while JCN
	&end
		POPr

RTN

@next-char ( addr* char -- addr* )
	
	STH
	&while
		LDAk STHkr = ,&end JCN
		LDAk #00 = ,&end JCN
		INC2 LDAk ,&while JCN
	&end
		POPr

RTN

( helpers )

@cswp ( string* a b -- )

	STH STH
	&while
		LDAk STHkr NEQ ,&continue JCN
			DUP2 OVRr STHr ROT ROT STA
			&continue
		INC2 LDAk ,&while JCN
	POP2 POP2r

RTN

@ccat ( char dst* -- )
	
	1--
	&while
		INC2 LDAk ,&while JCN
	STH2k STA
	#00 STH2r INC2 STA

RTN

@chex ( char -- hex )
	
	DUPk #2f GTH SWP #3a LTH #0101 == ,&num JCN
	DUPk #40 GTH SWP #47 LTH #0101 == ,&uca JCN
	DUPk #60 GTH SWP #67 LTH #0101 == ,&lca JCN
	POP #00 RTN
	&num ( char -- hex )
		LIT '0 - RTN
	&uca ( char -- hex )
		LIT 'A - #0a + RTN
	&lca ( char -- hex )
		LIT 'a - #0a + RTN

RTN

@scmp ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		LDAk LDAkr STHr #0000 !! ,&not-end JCN
			POP2 POP2r #01 RTN
			&not-end
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scmp-seg ( a* b* -- flag )

	STH2 
	&loop
		LDAk LDAkr STHr = ,&not-diff JCN
			POP2 POP2r #00 RTN
			&not-diff
		INC2k LDA #00 ! ,&continue JCN
			POP2 POP2r #01 RTN
			&continue
		INC2 INC2r
		,&loop JMP
	POP2 POP2r #00

RTN

@scat ( src* dst* -- )

	DUP2 ,slen JSR ++ ,scpy JSR

RTN

@scpy ( src* dst* -- )

	STH2 
	DUP2
	&while
		SWP2k -- STH2kr ++ STH2
		LDAk STH2r STA
		INC2 LDAk ,&while JCN
	SWP2 -- STH2r ++ 
	#00 ROT ROT STA

RTN

@slen ( addr* -- length* )

	1-- DUP2
	&while
		INC2 LDAk ,&while JCN
	SWP2 --
	1--

RTN

@mclr ( addr* length* -- )
	
	OVR2 ++ SWP2
	&loop
		DUP2 #00 ROT ROT STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN

@mcpy ( a* b* length* -- )
	
	STH2
	,&b STR2 ,&a STR2
	STH2r #0000
	&loop
		STH2k
		,&a LDR2 STH2kr ++ LDA 
		,&b LDR2 STH2r ++ STA
		INC2 GTH2k ,&loop JCN
	POP2 POP2

RTN
	&a $2 &b $2

@print-hex ( value* -- )
	
	&short ( value* -- )
		SWP ,&echo JSR 
	&byte ( value -- )
		,&echo JSR
	RTN

	&echo ( value -- )
	STHk #04 SFT ,&parse JSR .Console/write DEO
	STHr #0f AND ,&parse JSR .Console/write DEO
	RTN
	&parse ( value -- char )
		DUP #09 GTH ,&above JCN #30 + RTN &above #09 - #60 + RTN

RTN

@print-string ( addr* -- )

	1--
	&loop
		INC2 LDAk .Console/write DEO 
		LDAk ,&loop JCN
	POP2

RTN

@html-ext ".html $1
@htm-ext ".htm $1
@jpg-ext ".jpg $1

@title-txt "XXIIVV $1

@doctype "<!DOCTYPE 20 "html> $1
@html-tag "html $1
@head-tag "head $1
@title-tag "title $1
@body-tag "body $1
@header-tag "header $1
@nav-tag "nav $1
@main-tag "main $1
@footer-tag "footer $1
@img-tag "img $1
@a-tag "a $1
@span-tag "span $1
@link-tag "link $1
@ul-tag "ul $1
@li-tag "li $1
@figure-tag "figure $1
@figcaption-tag "figcaption $1

@target-attr "target $1
@src-attr "src $1
@alt-attr "alt $1
@href-attr "href $1
@blank-target "_blank $1
@rel-attr "rel $1
@type-attr "type $1

@year-txt "2021 $1
@stylesheet-txt "stylesheet $1
@shortcuticon-txt "shortcut 20 "icon $1
@license-txt "BY-NC-SA 20 "4.0 $1
@copy-entity-txt "&copy; $1
@mdash-entity-txt "&mdash; $1
@devinelulinvega-txt "Devine 20 "Lu 20 "Linvega $1
@creativecommon-alt-txt "CreativeCommons $1
@webring-alt-txt "Webring $1
@merveilles-alt-txt "Merveilles $1
@uxn-alt-txt "Uxn 20 "Powered $1
@http-txt "http $1

@lexicon-path "database/lexicon $1
@diary-path-page2 "database/diary2 $1
@diary-path-page1 "database/diary1 $1
@diary-path-page0 "database/diary0 $1
@input-path "content/ $1
@output-path "../site/ $1
@home-path "home.html $1
@stylesheet-path "../links/main.css $1
@about-path "about.html $1
@devinelulinvega-path "devine_lu_linvega.html $1
@creativecommon-path "https://creativecommons.org/licenses/by-nc-sa/4.0 $1
@webring-path "http://webring.xxiivv.com/ $1
@merveilles-path "https://merveilles.town/@neauoire $1
@uxn-path "uxn.html $1
@logo-path "../media/icon/logo.svg $1
@media-diary-path "../media/diary/ $1
@shortcuticon-path "../media/services/icon.png $1
@merveilles-icon-path "../media/icon/merveilles.svg $1
@creativecommon-icon-path "../media/icon/cc.svg $1
@webring-icon-path "../media/icon/webring.svg $1
@uxn-icon-path "../media/icon/uxn.svg $1

@no-picture "000 $1

@error-include-size "--include_size_exceeded $1
@error-broken-link "--redlink $1

@stylesheet-type "text/css $1
@shortcuticon-type "image/png $1

@buff $100

|1800